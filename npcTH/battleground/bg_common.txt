//===== rAthena Script ======================================= 
//= BattleGround System - Common NPCs
//===== By: ================================================== 
//= ????, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added VIP features and created a reward function. [Euphy]
//============================================================

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	419,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	419,{ end; }

bat_room,160,141,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "นักผจญภัยผู้ชาญฉลาด ทำไมคุณไม่ให้เรายืมพลังเพื่อชัยชนะล่ะ?";
	next;
	switch(select("สาเหตุของการรบคืออะไร:บอกฉันเกี่ยวกับ นายพล Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "กษัตริย์ผู้ยิ่งใหญ่ของ Maroll มาร์เซล มาโรลโลที่ 7 ป่วยหนักเมื่อเร็ว ๆ นี้";
		mes "ฝ่าบาททรงประกาศว่าเขาจะฝากอนาคตของ Marol ไว้กับข้าหรือเจ้าชายคนที่ 3 นายพล Guillaume.";
		next;
		mes "[Prince Croix]";
		mes "นายพล Guillaume อาจมีข้อได้เปรียบในการต่อสู้ครั้งนี้ เนื่องจากเขาเป็นแม่ทัพผู้ยิ่งใหญ่ของมาโรลล์ แต่นั่นไม่ได้หมายความว่าเขาจะชนะโดยอัตโนมัติ";
		mes " ฉันต้องการที่จะชนะการต่อสู้ครั้งนี้ เพื่อที่ฉันจะสามารถนำความเจริญรุ่งเรืองมาสู่ชาว Marol พวกเขาทนทุกข์ทรมานจากสงครามมามากพอแล้ว...";
		next;
		switch(select("ใช่ ฉันอยากเข้าร่วมกับคุณ:จบการสนทนา")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "ขอบคุณมาก. ฉันรู้สึกว่าฉันสามารถชนะได้ด้วยความช่วยเหลือจากนักผจญภัยเช่นคุณ";
			mes "ตอนนี้ กรุณาลงไปชั้นล่างและร่วมลับทักษะกับสหายของท่านเพื่อต่อสู้กับศัตรู!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "เพื่อ Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "เจ้าชายลำดับที่ 3 Guillaume เป็นแม่ทัพผู้ยิ่งใหญ่ของ Marol";
		mes "มันเสียเวลาที่จะอธิบายให้คุณฟังว่าเขาเป็นผู้นำหรือขุนศึกที่ยิ่งใหญ่ขนาดไหน เพราะเขาควบคุมอำนาจทางทหารอันยิ่งใหญ่ของ Marol";
		next;
		mes "[Prince Croix]";
		mes "น่าเสียดายที่มีบางอย่างที่เขาและผู้ติดตามไม่รู้:";
		mes "ชาว Marol ต้องการให้พวกเขาใช้เงินมากมายไปกับอำนาจทางการทหารจริงหรือ?";
		mes "เราได้รับความทุกข์ทรมานจากสงครามมามากพอแล้ว";
		mes "ฉันเชื่อว่าอาวุธไม่ใช่วิธีที่ดีที่สุดในการนำความเจริญรุ่งเรืองมาสู่ประเทศชาติ";
		next;
		mes "[Prince Croix]";
		mes "ฉันไม่ต้องการที่จะหลั่งเลือด แต่ฉันไม่มีทางเลือกอื่นนอกจากต้องต่อสู้เพื่อสันติภาพและเพื่อประโยชน์ของประชาชนของฉัน";
		next;
		switch(select("ใช่ ฉันอยากเข้าร่วมกับคุณ:จบการสนทนา")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "ขอบคุณมาก. ฉันรู้สึกว่าฉันสามารถชนะได้ด้วยความช่วยเหลือจากนักผจญภัยเช่นคุณ";
			mes "ตอนนี้ กรุณาลงไปชั้นล่างและร่วมลับทักษะกับสหายของท่านเพื่อต่อสู้กับศัตรู!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "เพื่อ Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	415,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "นักผจญภัยเลือดร้อน เราต้องการความสามารถของคุณเพื่อเอาชนะการต่อสู้ครั้งนี้";
	next;
	switch(select("สาเหตุของการรบคืออะไร:บอกฉันเกี่ยวกับ Prince Croix หน่อยสิ")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "กษัตริย์ผู้ยิ่งใหญ่ของเรา Marcel Marollo VII ทรงพระประชวรหนักเมื่อเร็ว ๆ นี้";
		mes "ฝ่าบาททรงประกาศว่าพระองค์ทรงเลือกฉันหรือเจ้าชายครัวซ์เป็นกษัตริย์องค์ต่อไปในบรรดาพระราชโอรสทั้ง 9 พระองค์";
		next;
		mes "[General Guillaume]";
		mes "สองกษัตริย์แบ่งชาติไม่ได้! มีเพียงผู้ที่ได้รับชัยชนะจากการต่อสู้ที่พระองค์ทรงกำหนดไว้เท่านั้นที่จะได้ขึ้นครองราชย์";
		next;
		mes "[General Guillaume]";
		mes "อย่างไรก็ตาม นี่ไม่ใช่แค่การต่อสู้ระหว่างเราเท่านั้น การต่อสู้ครั้งนี้จะเป็นตัวกำหนดอนาคตของประเทศนี้";
		mes "ฉันขอสาบานด้วยเกียรติของฉันที่จะพิสูจน์ว่าฉันคือผู้ที่สามารถปกป้อง Marol นี้จากการคุกคามจากภายนอก";
		next;
		switch(select("ใช่ ฉันอยากเข้าร่วมกับคุณ:จบการสนทนา")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "ยินดีต้อนรับสู่กองทัพของฉันสหาย";
		mes "ดวงตาของคุณบอกฉันว่าคุณเป็นทหารที่ฉันไว้ใจได้";
		next;
		mes "[General Guillaume]";
		mes "ตอนนี้ ขึ้นไปชั้นบนแล้วสมัครต่อสู้กับสหายของท่าน";
		mes "ฉันแน่ใจว่าพวกเขาจะต้อนรับคุณอย่างเต็มใจ!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes " ฉันจะเป็นคนยึดธงเอง!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "เจ้าชายครัวซ์ที่ 5 ปัจจุบันมีบรรดาศักดิ์เป็นนายกรัฐมนตรีแห่งมาโรลล์";
		mes "เขาคิดว่าเรื่องระดับชาติทั้งหมดของประเทศสามารถพูดคุยและตัดสินใจได้บนโต๊ะ และเชื่อในการอยู่ร่วมกันอย่างสันติกับประเทศอื่น ๆ";
		next;
		mes "[General Guillaume]";
		mes "เขาโง่เขลาเกินกว่าที่จะยอมรับว่าสิ่งที่เรียกว่าสันติภาพนั้นถูกสร้างขึ้นจากชีวิตจำนวนนับไม่ถ้วนที่ถูกสังเวยในสงคราม ในขณะที่ประชาชนทั่วไปและชนชั้นสูงสามารถมีชีวิตอยู่ได้ โดยไม่สนใจความน่าสะพรึงกลัวที่ทำให้พวกเขาดำเนินชีวิตแบบนั้น";
		next;
		mes "[General Guillaume]";
		mes "เขาไร้เดียงสาเกินกว่าจะเข้าใจความจริง....";
		mes "ฉันไม่สามารถทิ้ง Marol ให้กับคนอย่างเขาที่อาศัยอยู่ในความฝันได้!";
		next;
		mes "[General Guillaume]";
		mes "ความเชื่อที่ไม่สมจริงของเขาจะทำให้ประเทศนี้จมอยู่ในความยากจน และทำให้ประชาชนอ่อนแอ ถ้าเขาขึ้นเป็นกษัตริย์ มาโรลล์จะไม่มีวันหยุดพักจากการโจมตีของประเทศอื่น";
		mes "ฉันอยากจะสอนเขาว่าอะไรทำให้ประเทศเล็กๆ นี้มีพลังและเจริญรุ่งเรือง มันคืออำนาจทางการทหาร!";
		next;
		switch(select("ฉันอยากเข้าร่วมกองทัพของคุณ!:จบการสนทนา")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "ยินดีต้อนรับสู่กองทัพของฉันสหาย";
			mes "ดวงตาของคุณบอกฉันว่าคุณเป็นทหารที่ฉันไว้ใจได้";
			next;
			mes "[General Guillaume]";
			mes "ตอนนี้ ขึ้นไปชั้นบนแล้วสมัครการต่อสู้จากสหายของท่าน";
			mes "ฉันแน่ใจว่าพวกเขาจะต้อนรับคุณอย่างสุดใจ!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "ฉันจะเป็นคนยึดธงเอง!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	974,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	Teleporter#Battlefield	124,{
	mes "[Teleporter]";
	mes "คุณต้องการที่จะออกจากสนามรบหรือไม่? ใช้บริการของฉันเพื่อกลับไปยังเมือง";
	next;
	switch(select("ออก:ไม่ออก")) {
	case 1:
		mes "[Teleporter]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera.","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morocc Ruins.","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran.","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen.","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen.","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel.","rachel";
			setarray .@xy[0],115,124;
			break;
		case 8:
			setarray .@mapname$[0],"Morocc.","morocc";
			setarray .@xy[0],156,46;
			break;
		}
		mes "คุณจะถูกส่งกลับไปยัง "+.@mapname$[0]+".";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[Teleporter]";
		mes "ฉันจะอยู่ที่นี่เมื่อใดก็ตามที่คุณต้องการบริการของฉัน";
		close;
	}
	end;
}

-	script	Maroll Battle Recruiter::BatRecruit	728,{
	mes "[Maroll Battle Recruiter]";
	mes "สวัสดีนักผจญภัย";
	mes "ฉันเป็นอัศวินจากดินแดนอันไกลโพ้นที่เรียกว่าอาณาจักรมาโรล";
	next;
	mes "[Maroll Battle Recruiter]";
	mes "เจ้าชายทั้งสองแห่งอาณาจักรกำลังต่อสู้เพื่อชิงบัลลังก์แห่ง Marol และต้องการทหารที่มีประสบการณ์เช่นคุณ";
	mes "คุณอยากจะมอบอำนาจของคุณให้กับเจ้าชายคนหนึ่งในอาณาจักร Marol ได้อย่างไร?";
	next;
	switch(select("เข้าร่วม:ไม่เข้าร่วม")) {
	case 1:
		mes "[Maroll Battle Recruiter]";
		mes "ขอเทพเจ้าแห่งสงครามอวยพรคุณ";
		close2;
		getmapxy(.@mapname$,.@x,.@y,BL_NPC);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else if (.@mapname$ == "morocc")
			set bat_return,8;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Maroll Battle Recruiter]";
		mes "ฉันจะประจำการอยู่ที่นี่เสมอเพื่อรับทหารเพิ่ม รู้สึกอิสระที่จะกลับมาเมื่อใดก็ตามที่คุณสนใจ";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit7	728

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman
//============================================================
bat_room,138,144,4	script	Repairman#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "คำสั่งถูกยกเลิก";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "ฉันช่วยคุณได้ไหม?";
		next;
		switch(select("ปิดสนามรบ:เปิดสนามรบ:รีเซ็ต a01:รีเซ็ต b01:รีเซ็ต a02:รีเซ็ต b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Complete";
		close;
	}
}

// Badges Exchange
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน !! -";
		mes "- ขณะนี้คุณกำลังถือ -";
		mes "- มีของกับคุณมากเกินไป -";
		mes "- กรุณาลองใหม่อีกครั้ง -";
		mes "- หลังจากคุณลดน้ำหนักได้แล้ว -";
		close;
	}
	mes "[Erundek]";
	mes "คุณมีตราสนามรบไหม?";
	mes "ฉันสามารถแลกเปลี่ยน Bravery Badges และ Valor Badges เป็นไอเทมรางวัลได้";
	next;
	switch(select("แลกเปลี่ยน Badges:ตรวจสอบแค็ตตาล็อก")) {
	case 1:
		mes "[Erundek]";
		mes "คุณต้องการแลกเปลี่ยนไอเทมประเภทใด";
		mes "หากต้องการตรวจสอบข้อมูลเพิ่มเติมเกี่ยวกับไอเทมรางวัล โปรดใช้ ^3131FFCatalog^000000 ของเรา";
		next;
		switch(select("Weapon:Armor:Accessory:Consumable")) {
		case 1:
			mes "[Erundek]";
			mes "คุณเลือก ^3131FFWeapon^000000.";
			mes "อาวุธต่อไปนี้สามารถแลกเปลี่ยนกับ battlefield badges ได้";
			mes "โปรดทราบว่าไอเทมสำหรับ ^3131FFBravery Badges จะถูกระบุเป็น (BB)^000000, และ ^3131FFValor Badges เป็น (VB)^000000.";
			next;
			switch(select("Dagger/OneSword/TwoSword/TwoSpear:Staff/Mace/TwoAxe/Shuriken:Bow/Katar/Music/Whip:Book/Knuckle:Revolver/Rifle/Gatling/Shotgun/Launcher")) {
			case 1:
				mes "[Erundek]";
				mes "ไอเทมต่อไปนี้มีอยู่ในหมวด ^3131FFDagger, One-Handed Sword, Two-Handed Sword, และ Two-Handed Spear^000000.";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[Erundek]";
				mes "ไอเทมต่อไปนี้มีอยู่ในหมวด ^3131FFStaff / Mace / Two-Handed Axe / Huuma Shuriken^000000.";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[Erundek]";
				mes "อาวุธต่อไปนี้มีอยู่ในหมวด ^3131FFBow / Katar / Musical Instrument / Whip^000000.";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[Erundek]";
				mes "อาวุธต่อไปนี้มีอยู่ในหมวด ^3131FFBook / Knuckle^000000.";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[Erundek]";
				mes "อาวุธต่อไปนี้มีอยู่ในหมวด ^3131FFRevolver / Rifle / Gatling Gun / Shotgun / Grenade Launcher^000000.";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[Erundek]";
			mes "คุณเลือก ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000.";
			mes "คุณสามารถแลกเปลี่ยนไอเทมนี้กับ ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000.";
			mes "คุณต้องการแลกเปลี่ยนหรือไม่?";
			next;
			switch(select("ไม่แลกเปลี่ยน:แลกเปลี่ยน")) {
			case 1:
				break;
			case 2:
				mes "[Erundek]";
				mes "คุณต้องการใช้จ่าย ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 และรับ ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
				next;
				mes "[Erundek]";
				mes "โปรดจำไว้ว่า ไอเทมรางวัล Battleground คือ ^FF0000Character Bound^000000 คุณแน่ใจหรือไม่ว่าต้องการไอเทมนี้?";
				next;
				switch(select("ใช่:ไม่ใช่")) {
				case 1:
					mes "[Erundek]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "ขอบคุณสำหรับการแลกเปลี่ยน";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "ฉันขอโทษ คุณมีเหรียญตราไม่เพียงพอที่จะแลกเปลี่ยน";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "คุณต้องการเวลาเพิ่มเพื่อตรวจสอบไอเทมหรือไม่";
			close;
		case 2:
			mes "[Erundek]";
			mes "คุณเลือก ^3131FFArmor^000000.";
			mes "ชุดเกราะต่อไปนี้สามารถแลกเปลี่ยนกับ battlefield badges ได้";
			next;
			switch(select("Garments / Shoes:Armor")) {
			case 1:
				setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "คุณเลือก ^3131FFAccessory^000000.";
			mes "คุณสามารถแลกเปลี่ยน Medal of Honors เป็น Badges ของคุณตามสาย Job Class ได้ดังต่อไปนี้:";
			next;
			setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[Erundek]";
			mes "คุณเลือก ^3131FFConsumable^000000.";
			mes "ไอเทมสิ้นเปลืองต่อไปนี้สามารถแลกเปลี่ยนกับ battlefield badges ได้:";
			next;
			setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "เรามีไอเทมมากมาย ดังนั้นโปรดพิจารณาและซื้ออย่างตั้งใจ";
		close2;
		readbook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "คุณเลือก ^3131FF"+getitemname(.@Items[.@i])+"^000000.";
	switch(.@Items[.@i]) {
		case 2720: mes "ไอเทมนี้มีไว้สำหรับ Swordman และ Taekwon Master Class เท่านั้น"; break;
		case 2721: mes "ไอเทมนี้มีไว้สำหรับ Thief Class เท่านั้น"; break;
		case 2722: mes "ไอเทมนี้มีไว้สำหรับ Acolyte Class เท่านั้น"; break;
		case 2723: mes "ไอเทมนี้มีไว้สำหรับ Magician Class เท่านั้น"; break;
		case 2724: mes "ไอเทมนี้มีไว้สำหรับ Archer Class เท่านั้น"; break;
		case 2725: mes "ไอเทมนี้มีไว้สำหรับ Merchant Class เท่านั้น"; break;
		case 2733: mes "ไอเทมนี้มีไว้สำหรับ Gunslinger เท่านั้น"; break;
		default: break;
	}
	mes "คุณสามารถแลกเปลี่ยนไอเทมนี้กับ ^FF0000"+.@Items[.@i+1]+" "+getitemname(7828)+" หรือ "+.@Items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "คุณต้องการแลกเปลี่ยนหรือไม่?";
	next;
	switch(select("ไม่แลกเปลี่ยน:แลกเปลี่ยน")) {
	case 1:
		mes "[Erundek]";
		mes "คุณต้องการเวลาเพิ่มเพื่อตรวจสอบไอเทมหรือไม่";
		break;
	case 2:
		mes "[Erundek]";
		mes "คุณต้องการแลกเปลี่ยน Badge ใด";
		mes "คุณต้องมี ^3131FF"+.@Items[.@i+1]+" Badges^000000 เพื่อแลกเปลี่ยน";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "โปรดจำไว้ว่า ไอเทมรางวัล Battleground คือ ^FF0000Character Bound^000000 คุณแน่ใจหรือไม่ว่าต้องการไอเทมนี้?";
			next;
		}
		set .@j, select("Bravery Badge:Valor Badge:ยกเลิก");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "คุณยกเลิกการแลกเปลี่ยน";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@Items[.@i+1]) {
			mes "ขอบคุณสำหรับการแลกเปลี่ยน";
			delitem .@cost, .@Items[.@i+1];
			getitem .@Items[.@i],1;
		}
		else mes "คุณมี "+getitemname(.@cost)+" ไม่เพียงพอ";
		break;
	}
	close;
}

// Badge Reward Function
//============================================================
function	script	F_BG_Badge	{
	.@team$ = getarg(1);
	.@bg_type$ = getarg(2);

	if (.@bg_type$ == "Tierra") {
		set .@badge,7828; //BF_Badge1
		set .@amount_win,3;
		set .@amount_lose,1;
	} else if (.@bg_type$ == "Flavius") {
		set .@badge,7829; //BF_Badge2
		set .@amount_win,9;
		set .@amount_lose,3;
	} else
		return;

	if (getarg(0) == 1) {
		set .@amount, .@amount_win;
		if (.@team$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "Blessed Guillaume!";
		} else if (.@team$ == "Croix") {
			mes "[Swandery]";
			mes "Blessed Croix!";
		}
		mes "มาสนุกไปกับชัยชนะอันรุ่งโรจน์ของเรากันเถอะ!";
		mes strcharinfo(0)+", มันเป็นสัญญาณที่สะท้อนถึงชัยชนะ";
	} else {
		set .@amount, .@amount_lose;
		if (.@team$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "คุณแพ้ แต่คุณทุ่มเทให้กับการต่อสู้ครั้งนี้";
			mes "นี่คือรางวัลสำหรับความทุ่มเทอันยิ่งใหญ่ของคุณโดย Guillaume Marollo!";
			mes "จงเอาความพ่ายแพ้นี้เป็นบทเรียน และคราวหน้าเจ้าจะต้องชนะอย่างแน่นอน";
		} else if (.@team$ == "Croix") {
			mes "[Swandery]";
			mes "โอ้ "+strcharinfo(0)+" อย่าเศร้าเลย";
			mes "ถึงแม้ว่าเราจะไม่ชนะ แต่เราก็พยายามอย่างเต็มที่";
			mes "นี่คือของขวัญจาก Croix และโปรดอย่าลืมการต่อสู้ครั้งนี้ เราจะชนะในครั้งต่อไป";
		}
	}
	close2;

	// If the VIP system is enabled, VIP players receive an extra 2 badges.
	if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
		set .@amount, .@amount+2;

	set .@medal_gap, 500 - countitem(.@badge);
	if (.@medal_gap >= .@amount)
		getitem .@badge,.@amount;
	else
		getitem .@badge,.@medal_gap;
	return;
}