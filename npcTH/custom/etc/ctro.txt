//===== rAthena Script =======================================
//= Server
//===== By: ==================================================
//= SamMeePay (1.0)
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A bank which has an interest %
//===== Additional Comments: =================================
// สคลิป npc เฉพาะ Server
//============================================================

-	script	AllEXPRates	-1,{

OnHour00:
OnHour12:
	set $@brate,200;
	set $@jrate,200;
	set $@drate,200;
	
	//Base exp
	setbattleflag("base_exp_rate",$@brate);
	//Job exp
	setbattleflag("job_exp_rate",$@jrate);
	//Drops
	setbattleflag("item_rate_common",$@drate);
	setbattleflag("item_rate_heal",$@drate);
	setbattleflag("item_rate_use",$@drate);
	setbattleflag("item_rate_equip",$@drate,true);
	
	announce "อัตราปัจจุบันตอนนี้อยู่ที่: Base exp = "+($@brate/100)+"x Job exp = "+($@jrate/100)+"x Drops = "+($@drate/100)+"x",bc_all,0xFF6060;
	end;

OnHour06:
OnHour18:
	set $@brate,100;
	set $@jrate,100;
	set $@drate,100;

	//Base exp
	setbattleflag("base_exp_rate",$@brate);
	//Job exp
	setbattleflag("job_exp_rate",$@jrate);
	//Drops
	setbattleflag("item_rate_common",$@drate);
	setbattleflag("item_rate_heal",$@drate);
	setbattleflag("item_rate_use",$@drate);
	setbattleflag("item_rate_equip",$@drate,true);
	
	announce "อัตราปัจจุบันตอนนี้อยู่ที่: Base exp = "+($@brate/100)+"x Job exp = "+($@jrate/100)+"x Drops = "+($@drate/100)+"x",bc_all,0xFF6060;
	end;
}


// Script for Web CP UPDATE Status WOE
//===================================================================
//-	script	OnAgitStart	-1,{
//OnAgitStart:
//     query_sql "UPDATE woe_status SET status = 1";
//	 end;
//OnAgitEnd:
//	 query_sql "UPDATE woe_status SET status = 0";
//	 end;
//OnInit:
//        if(agitcheck()==1)
//                query_sql "UPDATE woe_status SET status = 1";
//        else
//                query_sql "UPDATE woe_status SET status = 0";
//		end;
//}


// Start NPC
//===================================================================

moc_para01,44,16,4	script	Lydia::npcexpc	4_F_05,{
	mes "[ "+strnpcinfo(1)+" ]";
	mes "เจ้ามี <ITEM>Poring Coin<INFO>7539</INFO></ITEM> มาให้ข้าไหม?";
	mes "ข้ามีของให้เจ้าแลกนะ แล้วเจ้าต้องการไรล่ะ...";
	next;
	switch(select(
			" ~ แลกของใช้งาน: ~ แลกของสวมใส่:~ อยากรู้เกี่ยวกับ Poring Coin:~ ^777777ยกเลิก^000000"
		)) {
		case 1:
			close2;
			callshop "Item_Consumable", 1;
			break;
		case 2:
			close2;
			callshop "Item_Equip", 1;
			break;
		case 3:
			mes "[ "+strnpcinfo(1)+" ]";
			mes "มอนสเตอร์แต่ละตัวบนเซิร์ฟเวอร์ของเราจะเหรียญโปริงมีโอกาสดรอป <ITEM>Poring Coin<INFO>7539</INFO></ITEM> 5%";
			mes "คุณยังสามารถรับสิ่งเหล่านี้ได้จากการทำภารกิจต่างๆ ในเกมและเข้าร่วมกิจกรรม";
			mes "การโยนเหรียญไม่ได้ผูกติดกับบัญชีหรือตัวละคร คุณสามารถขาย แลกเปลี่ยนกับผู้เล่นอื่น หรือโยนทิ้งไปเลยก็ได้";
			close;
		case 4:
			mes "[ "+strnpcinfo(1)+" ]";
			mes "งั้นมาใหม่นะ";
			close;
		}
}

moc_para01,53,33,4	script	รับซื้อการ์ด	90,{
	mes "[Card Trader]";
	mes "สวัสดี, "+strcharinfo(0)+"!";
	mes "ฉันจะทำอะไรให้คุณได้บ้าง?";
	next;
	switch(select(" > ขอข้อมูล: > ขายการ์ด: > ร้านค้า (^0055FF"+getd(.Points$)+"^000000): > ออก")) {
	case 1:
		mes "[Card Trader]";
		mes "คุณพบว่าคุณมี";
		mes "การ์ดไร้ประโยชน์ที่วางอยู่รอบๆ?";
		mes "ฉันยินดีที่จะถอดมันออก";
		mes "มือของคุณ!";
		next;
		mes "[Card Trader]";
		mes "ฉันจะให้ ^0055FF"+.Points[0]+" Point"+((.Points[0] == 1)?"":"s")+"^000000 สำหรับแต่ละรายการ";
		mes "การ์ดที่คุณให้ฉัน และ";
		mes "^0055FF"+.Points[1]+" Points^000000 สำหรับการ์ด MVP";
		mes "คุณสามารถแลกเปลี่ยนคะแนนเหล่านั้นได้";
		mes "สำหรับรายการในภายหลัง";
		mes "ว่าไง.?";
		close;
	case 2:
		mes "[Card Trader]";
		mes "เลือกการ์ดที่คุณ";
		mes "ต้องการแลกเปลี่ยน.";
		if (.Level) {
			mes " ";
			mes "ต้องทิ้ง";
			mes "โดยมอนสเตอร์ระดับ";
			mes .Level+" ขึ้นไป";
		}
		deletearray @sold_nameid[0],getarraysize(@sold_nameid);
		callshop "card_shop",2;
		npcshopattach "card_shop";
		end;
	case 3:
		mes "[Card Trader]";
		mes "คุณมี ^0055FF"+getd(.Points$)+"^000000 Point"+((getd(.Points$) == 1)?".":"s.");
		callshop "card_shop",1;
		npcshopattach "card_shop";
		end;
	case 4:
		mes "[Card Trader]";
		mes "*หาว*";
		mes "แล้วพบกันใหม่!";
		close;		
	}

OnSellItem:
	mes "Cards ที่จะขาย:";
	mes "-----------------------------------";
	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)
		if (@sold_nameid[.@i] > 4000 && @sold_nameid[.@i] < 4700) {
			if (.Level) {
				query_sql("SELECT `LV` FROM `mob_db` WHERE `DropCardid` = "+@sold_nameid[.@i],.@lv);
				if (.@lv < .Level) {
					dispbottom getitemname(@sold_nameid[.@i])+" อยู่ในระดับขั้นต่ำ";
					continue;
				}
			}
			set .@card_id[getarraysize(.@card_id)], @sold_nameid[.@i];
			set .@card_amt[getarraysize(.@card_amt)], @sold_quantity[.@i];
			set .@mvp, compare(.MVP$,""+@sold_nameid[.@i]);
			mes ((.@mvp)?"  ^FF0000":"  ^777777")+@sold_quantity[.@i]+"x "+getitemname(@sold_nameid[.@i])+"^000000";
			set .@card_total, .@card_total+(@sold_quantity[.@i]*((.@mvp)?.Points[1]:.Points[0]));
		}
	deletearray @sold_nameid[0], getarraysize(@sold_nameid);
	deletearray @sold_quantity[0], getarraysize(@sold_quantity);
	if (!.@card_id) {
		mes "  ^777777(ไม่มี)^000000";
		close;
	}
	mes " ";
	mes "---------- ทั้งหมด: ^0055FF"+.@card_total+" pt.^000000 -------";
	next;
	if(select(" > ^0055FFเสร็จสิ้นการซื้อขาย...^000000: > ^777777ยกเลิก^000000") == 2) {
		mes "[Card Trader]";
		mes "ตกลง...";
		close;
	}
	for(set .@i,0; .@i<getarraysize(.@card_id); set .@i,.@i+1)
		delitem .@card_id[.@i],.@card_amt[.@i];
	setd .Points$, getd(.Points$)+.@card_total;
	mes "[Card Trader]";
	mes "ทุกอย่างเสร็จเรียบร้อย!";
	close;

OnBuyItem:
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	if (.@cost > getd(.Points$)) {
		mes "[Card Trader]";
		mes "คุณมีคะแนนไม่เพียงพอ";
	}
	else {
		mes "รายการที่ซื้อ:";
		mes "-----------------------------------";
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			mes "  ^777777"+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+"^000000";
		}
		mes " ";
		mes "---------- ทั้งหมด: ^0055FF"+.@cost+" pt.^000000 -------";
		setd .Points$, getd(.Points$)-.@cost;
	}
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnInit:
	set .Level,0;   		// Minimum monster level to trade corresponding cards.
	set .Points$,"#Card_Points";	// Variable to store points.
	setarray .Shop[0],		// Card Shop items: <ID>,<point cost>
	  616,170,12103,120,7539,10;
	setarray .Points[0],10,50;	// Points per <normal card>,<MVP card>
	set .MVP$,			// List of MVP cards.
	  "4121,4123,4128,4131,4132,4134,4135,4137,4142,4143,4144,4145,4146,4147,4148,4168,4236,"+
	  "4241,4263,4276,4302,4305,4318,4324,4330,4342,4357,4359,4361,4363,4365,4399,4403,4407";
	
	npcshopdelitem "card_shop",909;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "card_shop",.Shop[.@i],.Shop[.@i+1];
	end;
}
-	shop	card_shop	-1,909:-1


-	script	LoginController	-1,{
OnPCLoginEvent:
	#CASHPOINTS = ##CASHPOINTS;
	end;
	
OnPCLogoutEvent:
	##CASHPOINTS = #CASHPOINTS;
	end;
}
