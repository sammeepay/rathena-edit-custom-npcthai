//===== rAthena Script =======================================
//= Hall of Abyss - Core Scripts
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Expansion for War of Emperium FE.
//= Invest in each realm to unlock new maps:
//= - Guild Dungeon F2, total 20,000,000z.
//= - Hall of Abyss, total 70,000,000z.
//===== Additional Comments: =================================
//= 1.0 First version. Missing official times. [Euphy]
//= 1.1 Added GM management function. [Euphy]
//============================================================

// Global Variables
//============================================================
// Note: an asterisk (*) represents any realm (prt, pay, gef, alde).
/*
$2011_agit_invest
-- 0: Inactive.
-- 1: Investment in progress.
-- 2: Investment results calculating.
-- 4: Investment calculations complete (only when at least one $*_invest_result is 3).
-- 11-21: Tie, revote required.

$agit_result_notice
-- 0: Inactive.
-- 1-12: Interim statements (displaying $@vfund_*x) available.

$agit_result_timer
-- 0: Inactive.
-- 0-59: Time elapsed during investment (in hours).

$agit_revoting_timer
-- 0: Inactive.
-- 0-29: Time elapsed during revote (in minutes).
-- 30: Revote closed.
-- 31: Revote results calculating.

$*_invest_result
-- 0: No data.
-- 1: Realm not connected.
-- 2: Guild Dungeon F2 open.
-- 3: Abyss Gate (Hall of Abyss and Gulid Dungeon F2) open.

$fund_*[01-05]
-- Investment units per guild castle.
$fund_*_extra
-- Investment units per realm.

$@vfund_*[01-05]
$@vfund_*_extra
-- Investment units, used in interim statements only.
*/

// Investment Functions & Controller
//============================================================
// Note: The times in this section are almost entirely custom.

-	script	#invest_timer	-1,{
OnClock0000:	// Open investments on Wednesday (1 hour after WoE)
	if (gettime(DT_DAYOFWEEK) == WEDNESDAY && !agitcheck()) {
		set $2011_agit_invest,1;
		donpcevent "#fund_master::OnInvest_start";
	}
	end;
OnClock1200:	// Close investments on Friday (60 hours after investments open)
	if (gettime(DT_DAYOFWEEK) == FRIDAY && !agitcheck()) {
		set $2011_agit_invest,2;
		donpcevent "#fund_master::OnInvest_stop";
	}
	end;
OnClock1235:	// Open dungeons on Friday (at least 31 minutes after investments close)
	if (gettime(DT_DAYOFWEEK) == FRIDAY && !agitcheck())
		donpcevent "#fund_master::OnResult";
	end;
OnClock2000:	// Close dungeons on Tuesday (1 hour before WoE)
	if (gettime(DT_DAYOFWEEK) == TUESDAY)
		donpcevent "#fund_master::OnReset";
	end;
}

function	script	F_Invest_Status	{
	set .@day, gettime(DT_DAYOFWEEK);
	set .@hour, gettime(DT_HOUR);
	set .@minute, gettime(DT_MINUTE);

	// Inactive.
	if (agitcheck())
		return 0;

	// Open for investments.
	if (.@day >= WEDNESDAY && (.@day < FRIDAY || (.@day == FRIDAY && .@hour <= 12)))
		return 1;

	// Investments closed, calculating results.
	if (.@day == FRIDAY && .@hour == 12 && .@minute < 35)
		return 2;

	// Calculations complete, dungeons open.
	if ((.@day == FRIDAY && (.@hour > 12 || (.@hour == 12 && .@minute >= 35))) || .@day > FRIDAY ||
	    .@day < TUESDAY || (.@day == TUESDAY && .@hour < 20))
		return 3;

	// Dungeons closed.
	return 0;
}

//callfunc "F_Invest_Abyss","<variable>","<NPC name>"{,"<addon message>"};
function	script	F_Invest_Abyss	{
	.@npc_name$ = getarg(1);
	mes "มี 2 วิธีในการลงทุน โดย Zeny หรือใบรับรองการลงทุนที่ออกโดย Cat Paw Merchant Guild";
	mes " คุณต้องการลงทุนด้วยวิธีไหน?";
	next;
	switch(select("ลงทุนกับ Zeny.:ลงทุนด้วยใบรับรองการลงทุน:ยกเลิก")) {
	case 1:
		mes .@npc_name$;
		mes "ดังนั้นคุณจึงตัดสินใจลงทุนใน Zeny";
		mes "หน่วยการลงทุนขั้นต่ำคือ ^4d4dff10,000 Zeny^000000";
		mes "คุณสามารถลงทุนได้สูงสุด 50 หน่วย ซึ่งเท่ากับ ^4d4dff500,000 Zeny^000000";
		next;
		switch(select("1 หน่วย 10,000 Zeny:10 หน่วย 100,000 Zeny:50 หน่วย 500,000 Zeny:จำนวนอื่น:ยกเลิก")) {
		case 1:
			set .@units,1;
			break;
		case 2:
			set .@units,10;
			break;
		case 3:
			set .@units,50;
			break;
		case 4:
			mes .@npc_name$;
			mes "กรุณากรอกจำนวนเงินที่คุณต้องการลงทุน";
			mes "กรุณาเลือกระหว่าง 1 ถึง 200";
			mes "กรุณาใส่ 0 เพื่อยกเลิก";
			next;
			input .@units;
			if (.@units < 0 || .@units > 200) {
				mes .@npc_name$;
				mes "คุณใส่เกินจำนวนต่อการลงทุน";
				mes "กรุณาลองอีกครั้ง";
				close;
			} else if (.@units == 0) {
				mes .@npc_name$;
				mes "ยกเลิกแล้ว";
				close;
			} else
				break;
		case 5:
			mes .@npc_name$;
			mes "ยกเลิกแล้ว";
			close;
		}
		set .@zeny, .@units * 10000;
		if (Zeny < .@zeny) {
			mes .@npc_name$;
			mes "คุณมีเงินไม่พอ";
			close;
		}
		mes .@npc_name$;
		mes "คุณได้ลงทุน "+callfunc("F_InsertPlural",.@units,"unit")+" "+callfunc("F_InsertComma",.@zeny)+" Zeny"+getarg(2,"")+" ";
		mes "ไม่มาก แต่นี่เป็นของขวัญเล็กๆ น้อยๆ คุณสามารถแลกเปลี่ยนกับไอเทมใน Malangdo ได้";
		set Zeny, Zeny-.@zeny;
		setd getarg(0), getd(getarg(0)) + .@units;
		getitem 6488,.@units; //Thanks_Invest_Ticket
		close;
	case 2:
		mes .@npc_name$;
		mes "ว้าว คุณได้นำใบรับรองการลงทุนมาด้วย";
		mes "คุณจะส่งใบรับรองกี่ใบ?";
		next;
		switch(select("1 Investment Certificate:5 Investment Certificates:จำนวนอื่น:ยกเลิก")) {
		case 1:
			set .@tickets,1;
			break;
		case 2:
			set .@tickets,5;
			break;
		case 3:
			mes .@npc_name$;
			mes "กรุณากรอกจำนวน Investment Certificates ที่คุณต้องการส่ง";
			mes "กรุณาเลือกระหว่าง 1 ถึง 20";
			mes "กรุณาใส่ 0 เพื่อยกเลิก";
			next;
			input .@tickets;
			if (.@tickets < 0 || .@tickets > 20) {
				mes .@npc_name$;
				mes "คุณใส่เกินจำนวนต่อการลงทุน";
				mes "กรุณาลองอีกครั้ง";
				close;
			} else if (.@tickets == 0) {
				mes .@npc_name$;
				mes "ยกเลิกแล้ว";
				close;
			} else
				break;
		case 4:
			mes .@npc_name$;
			mes "ยกเลิกแล้ว";
			close;
		}
		if (countitem(6489) < .@tickets) {
			mes .@npc_name$;
			mes "ดูเหมือนว่าคุณมี Investment Certificates น้อยกว่าที่คุณต้องการส่ง";
			close;
		}
		set .@units, .@tickets * 10;
		mes .@npc_name$;
		mes .@tickets+" Investment Certificate เท่ากับการลงทุน "+.@units+" หน่วย ("+((.@units < 100)?.@units:insertchar(""+.@units,",",1))+"0,000z)"+getarg(2,"")+".";
		mes "ไม่มาก แต่นี่เป็นของขวัญเล็กๆ น้อยๆ คุณสามารถแลกเปลี่ยนกับไอเทมใน Malangdo ได้";
		delitem 6489,.@tickets; //Cats_Invest_Certif
		setd getarg(0), getd(getarg(0)) + .@units;
		getitem 6488,.@units; //Thanks_Invest_Ticket
		close;
	case 3:
		mes .@npc_name$;
		mes "ยกเลิกแล้ว";
		close;
	}
}

// Script Core :: agit_fund
//============================================================
prt_gld,1,1,0	script	#fund_master	844,{
	end;
OnInit:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 1) {
		set $2011_agit_invest,1;
		end;
	}
	if ((.@invest_status == 2 && $2011_agit_invest == 1) ||
	    (.@invest_status == 3 && ($2011_agit_invest == 1 || $2011_agit_invest == 2))) {
		donpcevent "#fund_master::OnCalculate";
		end;
	}
	end;
OnCalculate_Election:
	set .@force_vote,1;
OnCalculate:
	setarray .@cas$[0],"prt","pay","gef","alde";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		// All funds per realm are summed.
		set .@funds[.@i], getd("$fund_"+.@cas$[.@i]+"01")+getd("$fund_"+.@cas$[.@i]+"02")+getd("$fund_"+.@cas$[.@i]+"03")+getd("$fund_"+.@cas$[.@i]+"04")+getd("$fund_"+.@cas$[.@i]+"05")+getd("$fund_"+.@cas$[.@i]+"_extra");

		// 70,000,000z opens Hall of Abyss and F2.
		if (.@funds[.@i] >= 7000) {
			setd "$"+.@cas$[.@i]+"_invest_result",3;
			set .@total_7000,1;
		}
		// 20,000,000z opens F2 and a chance for Hall of Abyss.
		else if (.@funds[.@i] >= 2000) {
			setd "$"+.@cas$[.@i]+"_invest_result",2;
			set .@total_2000,1;
		}
		// Anything below that opens nothing.
		else
			setd "$"+.@cas$[.@i]+"_invest_result",1;
	}
	// No further actions if 20,000,000z isn't reached.
	if (!.@total_2000)
		end;
	// No further actions if any realm hits 70,000,000z.
	if (.@total_7000) {
		set $2011_agit_invest,4;
		end;
	}
	// Otherwise, highest investment above 20,000,000z opens Hall of Abyss.
	set .@max, .@funds[0];
	for(set .@i,1; .@i<4; set .@i,.@i+1) {
		if (.@funds[.@i] > .@max) {
			set .@max, .@funds[.@i];
			set .@ele, .@i;
			deletearray .@rand$[0], getarraysize(.@rand$);
		} else if (.@funds[.@i] == .@max && .@funds[.@i] >= 2000) {
			if (!getarraysize(.@rand$))
				set .@rand$[0], .@cas$[.@ele];
			set .@rand$[getarraysize(.@rand$)], .@cas$[.@i];
		}
	}
	if (.@max < 2000)
		end;
	if (getarraysize(.@rand$)) {

		// Tie: declare a random winner.
		if (!.@force_vote) {
			set .@i, rand(getarraysize(.@rand$));
			setd "$"+.@rand$[.@i]+"_invest_result",3;
			set $2011_agit_invest,4;
			end;
		}

		// Tie: initiate a 30-minute election.
		set .@str$, implode(.@rand$,"|");
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (compare(.@str$,.@cas$[.@i]))
				set .@election, .@election | (1<<.@i);
		}
		switch(.@election) {
			case 3:  set $2011_agit_invest,11; break; //Prontera|Payon
			case 9:  set $2011_agit_invest,12; break; //Prontera|Al De Baran
			case 5:  set $2011_agit_invest,13; break; //Prontera|Geffen
			case 10: set $2011_agit_invest,14; break; //Al De Baran|Payon
			case 6:  set $2011_agit_invest,15; break; //Geffen|Payon
			case 12: set $2011_agit_invest,16; break; //Al De Baran|Geffen
			case 11: set $2011_agit_invest,17; break; //Prontera|Payon|Al De Baran
			case 7:  set $2011_agit_invest,18; break; //Prontera|Payon|Geffen
			case 13: set $2011_agit_invest,19; break; //Prontera|Al De Baran|Geffen
			case 14: set $2011_agit_invest,20; break; //Al De Baran|Payon|Geffen
			case 15: set $2011_agit_invest,21; break; //Prontera|Payon|Al De Baran|Geffen
		}
		if ($2011_agit_invest >= 11)
			donpcevent "Revoting Manager#Invest1::OnStart";
		end;
	} else {
		setd "$"+.@cas$[.@ele]+"_invest_result",3;
		set $2011_agit_invest,4;
		end;
	}
	end;
OnInvest_start:
	announce "จากนี้ไป เราจะเริ่มการลงทุนเพื่อเชื่อมต่อ 'Abyss Gate' ในพื้นที่ WOE: Valkyrie Realm, Greenwood Lake, Luina, Britoniah",bc_all,0xFF0000; //FW_NORMAL 10
	set $fund_prt01,0;
	set $fund_prt02,0;
	set $fund_prt03,0;
	set $fund_prt04,0;
	set $fund_prt05,0;
	set $fund_pay01,0;
	set $fund_pay02,0;
	set $fund_pay03,0;
	set $fund_pay04,0;
	set $fund_pay05,0;
	set $fund_gef01,0;
	set $fund_gef02,0;
	set $fund_gef03,0;
	set $fund_gef04,0;
	set $fund_gef05,0;
	set $fund_alde01,0;
	set $fund_alde02,0;
	set $fund_alde03,0;
	set $fund_alde04,0;
	set $fund_alde05,0;
	donpcevent "Investment_total#fund00::OnEnable";
	end;
OnInvest_stop:
	announce "เราได้เสร็จสิ้นการลงทุนเพื่อเชื่อมต่อ 'Abyss Gate' แล้ว",bc_all,0xFF0000; //FW_NORMAL 10
	donpcevent "Investment_total#fund00::OnReset";
	donpcevent "#fund_master::OnCalculate_Election";
	end;
OnResult:
	announce "เราได้ผลลัพธ์จากการลงทุนสำหรับ 'Abyss Gate' แล้ว สามารถตรวจสอบได้จากกระดานสถานะการลงทุนในแต่ละพื้นที่ WOE",bc_all,0xFF0000; //FW_NORMAL 10
	if ($2011_agit_invest == 4) {
		set $fund_prt01,0;
		set $fund_prt02,0;
		set $fund_prt03,0;
		set $fund_prt04,0;
		set $fund_prt05,0;
		set $fund_prt_extra,0;
		set $fund_pay01,0;
		set $fund_pay02,0;
		set $fund_pay03,0;
		set $fund_pay04,0;
		set $fund_pay05,0;
		set $fund_pay_extra,0;
		set $fund_gef01,0;
		set $fund_gef02,0;
		set $fund_gef03,0;
		set $fund_gef04,0;
		set $fund_gef05,0;
		set $fund_gef_extra,0;
		set $fund_alde01,0;
		set $fund_alde02,0;
		set $fund_alde03,0;
		set $fund_alde04,0;
		set $fund_alde05,0;
		set $fund_alde_extra,0;
	} else if ($2011_agit_invest == 2)
		donpcevent "#fund_master::OnCalculate";
	end;
OnReset:
	set $2011_agit_invest,0;
	donpcevent "Investment_total#fund00::OnReset";
	end;
}

prt_gld,2,2,0	script	Investment_total#fund00	844,{
	callfunc "F_GM_NPC";
	mes "นี่ทำงานไม่ถูกต้องใช่ไหม?";
	next;
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "หากคุณคิดว่าตัวจับเวลาการลงทุนนั้นตายแล้ว โปรดปรับเปลี่ยนตัวจับเวลา";
		next;
		switch(select("เปิดตัวจับเวลา: ยกเลิก")) {
		case 1:
			donpcevent "Investment_total#fund00::OnTimerReset";
			mes "คุณได้เปิดตัวจับเวลาแล้ว";
			close;
		case 2:
			mes "ยกเลิกแล้ว";
			close;
		}
	} else {
		mes "ฉันไม่จำเป็นต้องปรับแต่งอะไรตอนนี้";
		close;
	}

OnInit:
	if (callfunc("F_Invest_Status") == 1) {
		initnpctimer;
		donpcevent "Investment_total#fund00::OnVarInit";
	} else {
		set $agit_result_timer,0;
		set $agit_result_notice,0;
	}
	end;
OnVarInit:
	setarray .@cas$[0],"prt","pay","gef","alde";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		setd "$@vfund_"+.@cas$[.@i]+"01", getd("$fund_"+.@cas$[.@i]+"01");
		setd "$@vfund_"+.@cas$[.@i]+"02", getd("$fund_"+.@cas$[.@i]+"02");
		setd "$@vfund_"+.@cas$[.@i]+"03", getd("$fund_"+.@cas$[.@i]+"03");
		setd "$@vfund_"+.@cas$[.@i]+"04", getd("$fund_"+.@cas$[.@i]+"04");
		setd "$@vfund_"+.@cas$[.@i]+"05", getd("$fund_"+.@cas$[.@i]+"05");
		setd "$@vfund_"+.@cas$[.@i]+"_extra", getd("$fund_"+.@cas$[.@i]+"_extra");
	}
	end;
OnEnable:
	set $agit_result_timer,0;
	set $agit_result_notice,0;
	initnpctimer;
	end;
OnReset:
	set $agit_result_timer,0;
	set $agit_result_notice,0;
	stopnpctimer;
	end;
OnTimerReset:
	initnpctimer;
	end;
OnTimer3600000:
	if (callfunc("F_Invest_Status") == 1) {
		if ($agit_result_timer < 59) {
			set $agit_result_timer, $agit_result_timer+1;

			// Trigger on multiples of 5 up to 55, and 58.
			if ($agit_result_timer % 10 == 5 || $agit_result_timer % 10 == 0 || $agit_result_timer == 58) {
				set $agit_result_notice, $agit_result_notice+1;
				donpcevent "Investment_total#fund00::OnVarInit";
				announce "ขณะนี้มีใบแจ้งยอดระหว่างกาลฉบับที่ "+$agit_result_notice+" แล้ว สามารถตรวจสอบได้จากกระดานสถานะการลงทุนในแต่ละพื้นที่ WOE",bc_all,0xFF0000; //FW_NORMAL 10
			}

			donpcevent "Investment_total#fund00::OnTimerReset";
		} else
			stopnpctimer;
		end;
	} else {
		donpcevent "Investment_total#fund00::OnReset";
		end;
	}
}

prt_gld,164,98,3	script	Revoting Manager#Invest1	421,{
	mes "[Revoting Manager]";
	mes "เราไม่รับการโหวตเพิ่มเติม";
	mes "กรุณารอประกาศผลของเราก่อน";
	close;
OnInit:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 2 && $2011_agit_invest >= 11 && $agit_revoting_timer < 30) {
		donpcevent "Revoting Manager#Invest2::OnEnable";
		initnpctimer;
	} else if (.@invest_status == 3 && $2011_agit_invest >= 11)
		donpcevent "Revoting Manager#Invest1::OnCalculate";
	donpcevent "Revoting Manager#Invest1::OnDisable";
	end;
OnCalculate:
	switch($2011_agit_invest) {
		case 11: setarray.@cas$[0],"prt","pay"; break;
		case 12: setarray.@cas$[0],"prt","alde"; break;
		case 13: setarray.@cas$[0],"prt","gef"; break;
		case 14: setarray.@cas$[0],"alde","pay"; break;
		case 15: setarray.@cas$[0],"gef","pay"; break;
		case 16: setarray.@cas$[0],"alde","gef"; break;
		case 17: setarray.@cas$[0],"prt","pay","alde"; break;
		case 18: setarray.@cas$[0],"prt","pay","gef"; break;
		case 19: setarray.@cas$[0],"prt","alde","gef"; break;
		case 20: setarray.@cas$[0],"alde","pay","gef"; break;
		case 21: setarray.@cas$[0],"prt","pay","alde","gef"; break;
		default: end;
	}
	set .@max, getd("$fund_"+.@cas$[0]+"_extra");
	for(set .@i,1; .@i<getarraysize(.@cas$); set .@i,.@i+1) {
		set .@funds, getd("$fund_"+.@cas$[.@i]+"_extra");
		if (.@funds > .@max) {
			set .@max, .@funds;
			set .@ele, .@i;
			deletearray .@rand$[0], getarraysize(.@rand$);
		} else if (.@funds == .@max) {
			if (!getarraysize(.@rand$))
				set .@rand$[0], .@cas$[.@ele];
			set .@rand$[getarraysize(.@rand$)], .@cas$[.@i];
		}
	}
	if (getarraysize(.@rand$)) {
		set .@i, rand(getarraysize(.@rand$));
		setd "$"+.@rand$[.@i]+"_invest_result",3;
	} else
		setd "$"+.@cas$[.@ele]+"_invest_result",3;
	set $2011_agit_invest,4;
	set $agit_revoting_timer,0;
	end;
OnEnable:
	enablenpc "Revoting Manager#Invest1";
	end;
OnDisable:
	disablenpc "Revoting Manager#Invest1";
	end;
OnStart:
	initnpctimer;
	donpcevent "Revoting Manager#Invest2::OnNotice_on";
	end;
OnEnd:
	stopnpctimer;
	disablenpc "Revoting Manager#Invest1";
	end;
OnTimerReset:
	initnpctimer;
	end;
OnTimer60000:
	set .@revote, $agit_revoting_timer;
	if (.@revote < 30) {
		set $agit_revoting_timer, $agit_revoting_timer+1;
		donpcevent "Revoting Manager#Invest1::OnTimerReset";
	} else if (.@revote == 30) {
		announce "Cat Paw Merchant Guild: ตอนนี้เรากำลังจะปิดการโหวตเพิ่มเติม เราจะประกาศผลเร็วๆ นี้",bc_all,0xFF0000; //FW_NORMAL 10
		donpcevent "Revoting Manager#Invest2::OnDisable";
		donpcevent "Revoting Manager#Invest1::OnEnable";
		set $agit_revoting_timer, $agit_revoting_timer+1;
		donpcevent "Revoting Manager#Invest1::OnTimerReset";
	} else if (.@revote == 31) {
		donpcevent "Revoting Manager#Invest1::OnCalculate";
		donpcevent "Revoting Manager#Invest1::OnDisable";
		stopnpctimer;
		end;
	} else {
		donpcevent "Revoting Manager#Invest1::OnDisable";
		stopnpctimer;
	}
	end;
}

prt_gld,164,98,3	script	Revoting Manager#Invest2	421,{
	set .@invest, $2011_agit_invest;
	if (.@invest < 11) {
		mes "[Revoting Manager]";
		mes "ไม่! นี่ไม่ใช่เวลาที่จะเพิกถอน";
		close2;
		disablenpc "Revoting Manager#Invest2";
		end;
	}
	set .@playtime, checkquest(7349,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[Revoting Manager]";
		mes "คุณได้มีส่วนร่วมในการโหวตแล้ว";
		mes "กรุณาผ่อนคลายและรอผล";
		close;
	} else if (.@playtime == 2)
		erasequest 7349;
	mes "[Revoting Manager]";
	mes "คุณต้องการมีส่วนร่วมในการโหวตเพื่อเลือกสถานที่ที่จะเชื่อมต่อ 'Abyss Gate' หรือไม่?";
	next;
	if(select("มีส่วนร่วมในการโหวต:ข้ามไปก่อน") == 2) {
		mes "[Revoting Manager]";
		mes "คุณได้ยกเลิกการโหวตแล้ว";
		close;
	}
	mes "[Revoting Manager]";
	mes "คุณสามารถลงคะแนนได้ครั้งเดียวและไม่ต้องลงทุนสำหรับการเลือกของคุณ"; 
	mes "โปรดเลือกหนึ่งพื้นที่ WOE";
	next;

	// Create menu.
	explode(.@candidates$, callsub(OnGetCandidates, .@invest),"|");
	for(set .@i,0; .@i<getarraysize(.@candidates$); set .@i,.@i+1) {
		if (.@candidates$[.@i] == "alde")
			set .@menu$[.@i],"Luina";
		else if (.@candidates$[.@i] == "gef")
			set .@menu$[.@i],"Britoniah";
		else if (.@candidates$[.@i] == "pay")
			set .@menu$[.@i],"Greenwood Lake";
		else if (.@candidates$[.@i] == "prt")
			set .@menu$[.@i],"Valkyrie Realm";
	}
	set .@vote, select(implode(.@menu$,":"))-1;

	mes "[Revoting Manager]";
	mes "คุณได้โหวตให้กับ "+.@menu$[.@vote]+"";
	mes "ขอบคุณสำหรับการโหวต";
	setquest 7349;
	setd "$fund_"+.@candidates$[.@vote]+"_extra", getd("$fund_"+.@candidates$[.@vote]+"_extra") + 1;
	close;
OnGetCandidates:
	switch(getarg(0)) {
		case 11: return "prt|pay";
		case 12: return "prt|alde";
		case 13: return "prt|gef";
		case 14: return "alde|pay";
		case 15: return "gef|pay";
		case 16: return "alde|gef";
		case 17: return "prt|pay|alde";
		case 18: return "prt|pay|gef";
		case 19: return "prt|alde|gef";
		case 20: return "alde|pay|gef";
		case 21: return "prt|pay|alde|gef";
	}
	return "";
OnInit:
	disablenpc "Revoting Manager#Invest2";
	end;
OnEnable:
	enablenpc "Revoting Manager#Invest2";
	end;
OnDisable:
	disablenpc "Revoting Manager#Invest2";
	stopnpctimer;
	end;
OnNotice_on:
	initnpctimer;
	end;
OnTimer5000:
	announce "Cat Paw Merchant Guild: เนื่องจากไม่มีพื้นที่ WOE ถึงระดับการลงทุนสูงสุดเพื่อเชื่อมต่อ 'Abyss Gate'...",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer8000:
	announce "Cat Paw Merchant Guild: เรากำลังจะเชื่อมต่อ 'Abyss Gate' กับพื้นที่ WOE ที่ลงทุนมากที่สุด",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer11000:
	announce "Cat Paw Merchant Guild: อย่างไรก็ตาม มีพื้นที่ที่ลงทุนจำนวนเท่ากัน ดังนั้นเราจะให้พวกเขาลงคะแนนเสียง",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer14000:
	announce "Cat Paw Merchant Guild: สามารถโหวตเพิ่มเติมได้เป็นเวลา 30 นาที คุณสามารถโหวตได้ในอาณาจักรวาลคิรี",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer20000:
	explode(.@candidates$, callsub(OnGetCandidates, $2011_agit_invest),"|");
	for(set .@i,0; .@i<getarraysize(.@candidates$); set .@i,.@i+1) {
		if (.@candidates$[.@i] == "alde")
			set .@choices$[.@i],"Luina";
		else if (.@candidates$[.@i] == "gef")
			set .@choices$[.@i],"Britoniah";
		else if (.@candidates$[.@i] == "pay")
			set .@choices$[.@i],"Greenwood Lake";
		else if (.@candidates$[.@i] == "prt")
			set .@choices$[.@i],"Valkyrie Realm";
	}
	announce "Cat Paw Merchant Guild: ผู้สมัครชิงคะแนนเสียงเพิ่มเติมคือ... "+implode(.@choices$," vs. ")+"",bc_all,0xFF0000; //FW_NORMAL 10
	set $fund_prt_extra,0;
	set $fund_pay_extra,0;
	set $fund_gef_extra,0;
	set $fund_alde_extra,0;
	set $agit_revoting_timer,0;
	donpcevent "Revoting Manager#Invest2::OnEnable";
	stopnpctimer;
	end;
}

/*
prtg_cas01,2,2,0	script	Abrai	55,{
	mes " ฉันควรลาออกจากงานสจ๊วตแล้ว";
	close;
OnAgitInvest:
	switch(callfunc("F_Invest_Status")) {
	case 0:
		set $2011_agit_invest,0;
		donpcevent "#fund_master::OnReset";
		break;
	case 1:
		set $2011_agit_invest,1;
		donpcevent "#fund_master::OnInvest_start";
		break;
	case 2:
		set $2011_agit_invest,2;
		donpcevent "#fund_master::OnInvest_stop";
		break;
	case 3:
		donpcevent "#fund_master::OnResult";
		break;
	}
	end;
}
*/

// Realms - Merchant Guilds, Gate Managers, Status Boards
//============================================================
-	script	Cat Paw Merchant Guild	-1,{

	if (compare(strnpcinfo(4),"alde")) {
		set .@npc$,"alde";
		set .@name$,"Megrez";
		set .@realm$,"Luina";
		set .@intro$,"ผู้จัดการสาขา Luina ใน Al De Baran";
	} else if (compare(strnpcinfo(4),"gef")) {
		set .@npc$,"gef";
		set .@name$,"Meraq";
		set .@realm$,"Britoniah";
		set .@intro$,"ผู้จัดการสาขา Britoniah";
	} else if (compare(strnpcinfo(4),"pay")) {
		set .@npc$,"pay";
		set .@name$,"Phecda";
		set .@realm$,"Greenwood Lake";
		set .@intro$,"ผู้จัดการสาขา Payon Greenwood Lake";
	} else if (compare(strnpcinfo(4),"prt")) {
		set .@npc$,"prt";
		set .@name$,"Dubae";
		set .@realm$,"Valkyrie Realm";
		set .@intro$,"ผู้จัดการสาขาอาณาจักร Prontera Valkyrie";
	}
	set .@n$,"["+.@name$+"]";

	mes .@n$;
	mes "ฉันชื่อ "+.@name$+", เป็น "+.@intro$+" ของสมาคมพ่อค้าอุ้งตีนแมว";
	mes "ฉันปรึกษาเรื่องการลงทุนเพื่อการพัฒนา ประตูนรก";
	mes "ฉันจะช่วยคุณได้อย่างไร?";
	next;
	switch(select("ประตูนรกคืออะไร?:ลงทุนพัฒนาประตู?:ฉันต้องการลงทุนในพื้นที่นี้:ยกเลิก")) {
	case 1:
		mes .@n$;
		mes "เพื่อความสะดวกเรียกว่า ประตูนรก เท่านั้น แต่เป็นเพียง ^4d4dffช่องว่างสู่อีกมิติหนึ่ง^000000.";
		next;
		mes .@n$;
		mes "พบช่องว่างเหล่านี้มากขึ้นที่นี่และที่นั่นหลังจากที่ Morocc สร้าง Crack of Dimension";
		mes "โดยส่วนตัวแล้วผมคิดว่า ประตูนรก ที่เพิ่งค้นพบนั้นเป็นส่วนหนึ่งของอดีตหรือแม้แต่จากอนาคตด้วยซ้ำ";
		next;
		mes .@n$;
		mes "ว่ากันว่า ประตูนรก เชื่อมโยงอย่างเข้มแข็งกับอีกมิติหนึ่งที่อยู่นอกเหนือโลกของเรา";
		mes "อาจเป็นเพราะมีผู้ที่ติดตามอำนาจของ Emperium";
		next;
		mes .@n$;
		mes " จนถึงตอนนี้มีประตูนรก ทั้งหมด 4 ประตูที่ถูกค้นพบ พวกมันเชื่อมต่อกับพื้นที่ Agit ซึ่งเต็มไปด้วยพลังของ Emperium";
		next;
		mes .@n$;
		mes "เราตั้งชื่อแต่ละมิติเหนือช่องว่าง";
		mes "^4d4dffวิถีแห่งนักรบ, เนินเขาแห่งความตาย, สายลมแห่งสมัยโบราณ และน้ำตาของวีรบุรุษ^000000";
		mes "และช่องว่างทั้งสี่นี้เรียกว่า ^4d4dffประตูนรก^000000.";
		next;
		mes .@n$;
		mes "ช่องว่างอาจนำไปสู่อดีต อนาคต หรือแม้แต่โลกที่ไม่มีใครรู้จัก";
		mes "พลังที่ ประตูนรก ครอบครองนั้นทั้งน่ากลัวแต่ก็ไพเราะ";
		next;
		mes .@n$;
		mes "คุณสนใจที่จะสำรวจ ประตูนรก หรือไม่?";
		mes "ฮ่าฮ่า คุณจะต้องผ่านหลายขั้นตอนก่อนจะสำรวจ";
		next;
		mes .@n$;
		mes "ถ้าคุณสนใจ";
		mes "คุณพอจะมีเวลานั่งลงและพูดคุยเกี่ยวกับการลงทุนในการบำรุงรักษา ประตูนรก ไหม?";
		close;
	case 2:
		mes .@n$;
		mes "คุณอยากรู้เกี่ยวกับการลงทุนในการพัฒนา ประตู หรือไม่?";
		mes "คุณอยากรู้เรื่องอะไร?";
		next;
		switch(select("เกี่ยวกับประตู?:เกี่ยวกับการลงทุน?:ลงทุนอย่างไร?:ประโยชน์จากการลงทุน?")) {
		case 1:
			mes .@n$;
			mes "ประตูคือสิ่งที่ถูกตั้งชื่อตาม ประตู";
			mes "ทางเดินมิติที่เชื่อมต่อจากที่นี่ไปที่นั่น";
			next;
			mes .@n$;
			mes "ประตู Abyss ถูกสร้างขึ้นเพื่อเป็นเส้นทางที่ไม่แน่นอนจากช่องว่างมิติ";
			mes "ดังนั้นการเดินทางผ่านประตูจะต้องเชื่อมต่อมิติเทียม";
			next;
			mes .@n$;
			mes "มนุษย์ยังห่างไกลจากการมีเทคโนโลยีขั้นสูงที่จำเป็น";
			mes "แต่ Cat Paw Merchant Guild ของเรามีเทคโนโลยีชั้นสูงเพื่อใช้ Crack of Dimension";
			next;
			mes .@n$;
			mes "ดังนั้นเราจึงได้ยื่นข้อเสนอ";
			mes "ด้วยสัญญาว่าจะสนับสนุน Cat Paw Merchant Guild และจ่ายค่าใช้จ่ายจำนวนมหาศาลเพื่อรักษาการเชื่อมต่อกับประตู";
			mes "เราจะเชื่อมต่อกับ Crack of Dimension";
			next;
			mes .@n$;
			mes "แน่นอนว่าการทำงานกับ Crack of Dimension ไม่ใช่เรื่องง่าย และมันมาพร้อมกับความเสี่ยง";
			mes "มีหลายกรณีที่ผู้คนหายไปจากการเดินทางระหว่าง";
			next;
			mes .@n$;
			mes "ระยะเวลาการรักษาที่ดีที่สุดคือประมาณ 4 วัน";
			mes "คุณลงทุนกับเราและเราสร้างประตูที่เชื่อมต่อกับช่องว่างมิติ";
			next;
			mes .@n$;
			mes "ฟังดูเป็นข้อเสนอ win-win ที่สมบูรณ์แบบไม่ใช่เหรอ?";
			close;
		case 2:
			mes .@n$;
			mes "การบังคับผ่านช่องว่างมิติที่ไม่เสถียรและสร้างทางเดินนั้นอันตรายมาก";
			mes "ไม่ต้องพูดถึงทรัพยากรจำนวนมหาศาลที่จำเป็น";
			next;
			mes .@n$;
			mes "ถ้าคุณดูแลค่าใช้จ่ายและอันตรายที่จ่ายโดยการลงทุนใน Cat Paw Merchant Guild";
			mes "เราจะสร้างและบำรุงรักษาประตูโดยไม่คำนึงถึงความเสี่ยง";
			next;
			mes .@n$;
			mes "แต่แน่นอนว่าเงินที่เกี่ยวข้องกับค่าประตูจะสูงมาก";
			mes "มันจะง่ายถ้าคุณสามารถหานักลงทุนมาแบ่งปันจำนวนนี้ได้";
			next;
			mes .@n$;
			mes "มีค่าใช้จ่ายพื้นฐานในการเริ่มเชื่อมต่อประตู";
			mes "ผลลัพธ์ขึ้นอยู่กับจำนวนเงินที่ลงทุนที่รวบรวมได้";
			next;
			mes .@n$;
			mes "ประตูนรก มีอยู่ในแต่ละภูมิภาค Agit";
			mes "คุณลองจินตนาการดูไหมว่ากิลด์ของเราจะต้องใช้เงินและความเสียสละจำนวนเท่าใดเพื่อพัฒนาในทุกด้าน";
			next;
			mes .@n$;
			mes "สิ่งนี้จะอธิบายการแข่งขันด้านการลงทุนในระดับภูมิภาค";
			mes "หากรวบรวมค่าบำรุงรักษาประตูได้ไม่เพียงพอ ก็ชัดเจนว่าภูมิภาคที่มีการลงทุนมากที่สุดจะได้รับผลประโยชน์";
			next;
			mes .@n$;
			mes "หาก ^4d4dffเชื่อมต่อ ประตูนรก สำเร็จ แสดงว่าดันเจี้ยนของพื้นที่ซ่อนตัว";
			mes "จะเปิดให้คนอื่นๆ แทนที่จะเป็นกิลด์พิเศษ^000000 ซึ่งจะน่าสนใจมากสำหรับการลงทุน";
			next;
			mes .@n$;
			mes "แต่ขอย้ำอีกครั้งว่าใช้ได้เฉพาะเมื่อเชื่อมต่อประตูเท่านั้น";
			close;
		case 3:
			mes .@n$;
			mes "มันง่ายมาก";
			mes "เยี่ยมชมผู้จัดการสาขาของ ^4d4dffภูมิภาคที่คุณต้องการลงทุนในช่วงระยะเวลาการลงทุน^000000 และทำการลงทุนของคุณ";
			next;
			mes .@n$;
			mes "เรามักจะได้รับ ^4d4dffสกุลเงินทั่วไปที่นี่ Zeny^000000";
			mes "แต่เนื่องจากเรากำลังพูดถึงการสนับสนุน Cat Paw Merchant Guild ของเรา คุณจึงสามารถลงทุนกับสินค้าที่เป็นที่ต้องการใน Malangdo ได้";
			next;
			mes .@n$;
			mes "มอบสิ่งของที่ต้องการให้กับ Namis ใน Malangdo ซึ่งรับผิดชอบงานทั่วไปในกิลด์ของเรา";
			mes "นำใบรับรองการลงทุนของคุณกลับมาเพื่อเพิ่มให้กับการลงทุน";
			next;
			mes .@n$;
			mes "คุณต้องเยี่ยมชมและลงทุนในภูมิภาคที่คุณต้องการเพื่อที่การลงทุนของคุณจะถูกนับเป็นเครื่องบรรณาการ";
			close;
		case 4:
			mes .@n$;
			mes "โดยพื้นฐานแล้ว ประตูนรก นั้นหดตัวและเชื่อมต่อกับปราสาทที่เป็นเจ้าของ Agit;";
			mes "ดังนั้น เราจะจัดหาทุกอย่างที่เรามีเมื่อค่าใช้จ่ายในการเชื่อมต่อและบำรุงรักษาประตูมีเพียงพอแล้ว";
			next;
			mes .@n$;
			mes "ในแง่ของต้นทุนขั้นต่ำและสูงสุดสำหรับงานทั้งหมด";
			mes "หากการลงทุนที่รวบรวมได้ ^4d4dffเกินค่าใช้จ่ายขั้นต่ำ^000000 เราสามารถเชื่อมต่อคุณกับชั้น ^4d4dffที่ลึกที่สุดของดันเจี้ยนกิลด์ที่ไม่รู้จัก^000000";
			next;
			mes .@n$;
			mes "หากการลงทุนเกินต้นทุนสูงสุด ภูมิภาคนั้นจะเชื่อมต่อกับ";
			mes "^4d4dffประตูนรก^000000 โดยไม่คำนึงถึงอันดับการลงทุนซึ่งรวมถึงชั้นที่ลึกที่สุดของดันเจี้ยนกิลด์";
			next;
			mes .@n$;
			mes "^ff0000หากไม่มีภูมิภาค Agit ที่เข้าถึงการลงทุนสูงสุด ดังนั้นภูมิภาคที่มี";
			mes "การมีส่วนร่วมในการลงทุนสูงสุดจะทำให้ ประตูนรก เชื่อมโยงกับการลงทุนที่รวบรวมจากทุกภูมิภาค^000000";
			next;
			mes .@n$;
			mes "เมื่อ ประตูนรก เชื่อมต่อกัน ชั้นที่ลึกที่สุด (2F) ของดันเจี้ยนกิลด์";
			mes "จะเปิดให้นักผจญภัยทุกคนที่เข้าร่วมในการลงทุนระดับภูมิภาค";
			next;
			mes .@n$;
			mes "การเยี่ยมชมดันเจี้ยนจะถูกนับสำหรับการลงทุนครั้งต่อไปสำหรับภูมิภาค Agit";
			mes "ดังนั้นการมีความสัมพันธ์ที่ดีกับกิลด์ที่อ้างว่าปราสาท Agit จะเป็นโบนัส";
			next;
			mes .@n$;
			mes "ยังไงก็ตาม มันไม่ง่ายเลยที่จะรักษาการเชื่อมต่อกับ ประตูนรก และจำเป็นต้องมีการลงทุนและการเชื่อมต่อใหม่ทุกสัปดาห์";
			mes "ดังนั้นโปรดสนับสนุนและลงทุนต่อไปถ้าคุณต้องการพลังแห่งความไม่รู้";
			close;
		}
	case 3:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
			mes "- ขณะนี้คุณกำลังถือ -";
			mes "- มีของกับคุณมากเกินไป -";
			mes "- กรุณาลองอีกครั้งหลังจากที่คุณ -";
			mes "- ลดน้ำหนักหน่อย -";
			close;
		}
		if (countitem(6488) >= 4800) {
			mes .@n$;
			mes "คุณมี Thank Invest Tickets มากเกินไปที่จะรับเพิ่ม";
			mes "ไม่สามารถให้ตั๋วแก่คุณได้อีกต่อไป โปรดใช้ตั๋วของคุณและกลับมา";
			mes "ตั๋วสามารถแลกเปลี่ยนกับไอเทมต่างๆ ใน Malangdo";
			close;
		}
		if (callfunc("F_Invest_Status") != 1) {
			mes .@n$;
			mes "เราไม่ได้รับการลงทุนในขณะนี้";
			mes "กรุณากลับมาในระหว่างระยะเวลาการลงทุน";
			close;
		}
		set .@guild, getcharid(2);
		for(set .@i,1; .@i<=5; set .@i,.@i+1) {
			set .@castle_name$[.@i], getcastlename(.@npc$+"g_cas0"+.@i);
			set .@owner_id[.@i], getcastledata(.@npc$+"g_cas0"+.@i,CD_GUILD_ID);
			set .@owner_name$[.@i], getguildname(.@owner_id[.@i]);
			if (.@guild == .@owner_id[.@i]) {
				set .@menu$, .@menu$+.@castle_name$[.@i]+" ["+.@owner_name$[.@i]+"] Guild:";
				set .@castles_owned, .@castles_owned | (1<<.@i);
			} else
				set .@menu$, .@menu$+"^aaaaaaไม่ใช่กิลด์ที่อ้างสิทธิ์ "+.@castle_name$[.@i]+"^000000:";
		}
		mes .@n$;
		mes "คุณต้องการลงทุนในภูมิภาค ^4d4dff"+.@realm$+"^000000 หรือไม่?";
		mes "หากมีปราสาทที่ถูกครอบครองอยู่แล้ว คุณจะต้องลงทุนในปราสาทและกิลด์ที่ครอบครองปราสาทนั้น";
		next;
		if (.@castles_owned) {
			set .@i, select(.@menu$+"ยกเลิก");
			if (.@i == 6) {
				mes .@n$;
				mes "ยกเลิก.";
				close;
			}
			if (.@guild != .@owner_id[.@i]) {
				mes .@n$;
				mes "กิลด์ที่กำลังครอบครองอยู่ "+.@castle_name$[.@i]+" คือ "+.@owner_name$[.@i]+".";
				mes "ดูไม่เหมือนกิลด์ของคุณเลย";
				close;
			}
			mes .@n$;
			mes "ลงทุนภายใต้ชื่อกิลด์ "+.@castle_name$[.@i]+"'s"+.@owner_name$[.@i]+".";
			callfunc "F_Invest_Abyss","$fund_"+.@npc$+"0"+.@i,.@n$;
			end;
		} else {
			mes .@n$;
			callfunc "F_Invest_Abyss","$fund_"+.@npc$+"_extra",.@n$," for "+.@realm$;
			end;
		}
	case 4:
		mes .@n$;
		mes "อย่างนั้นเหรอ?";
		mes "โปรดกลับมาทุกครั้งที่คุณสนใจที่จะลงทุนในการพัฒนา ประตูนรก";
		close;
	}
}

-	script	Gate Manager#realm	-1,{

	if (compare(strnpcinfo(4),"alde")) {
		set .@npc$,"alde";
		set .@name$,"Alkor";
		set .@gate$,"Hero's Tears";
		set .@realm$,"Luina";
	} else if (compare(strnpcinfo(4),"gef")) {
		set .@npc$,"gef";
		set .@name$,"Mizar";
		set .@gate$,"Hill of the Dead";
		set .@realm$,"Britoniah";
	} else if (compare(strnpcinfo(4),"pay")) {
		set .@npc$,"pay";
		set .@name$,"Alkaid";
		set .@gate$,"Winds of the Ancient";
		set .@realm$,"Greenwood Lake";
	} else if (compare(strnpcinfo(4),"prt")) {
		set .@npc$,"prt";
		set .@name$,"Arios";
		set .@gate$,"Way of the Warrior";
		set .@realm$,"Valkyrie Realm";
	}
	set .@n$,"["+.@name$+"]";

	if (callfunc("F_Invest_Status") != 3) {
		mes .@n$;
		mes "ขณะนี้ไม่มีประตูดันเจี้ยนที่เชื่อมต่ออยู่";
		close;
	}
	set .@status, getd("$"+.@npc$+"_invest_result");
	if (.@status == 3) {
		mes .@n$;
		mes "เราประสบความสำเร็จในการเชื่อมต่อประตูนรก ["+.@gate$+"] ด้วยความช่วยเหลือจากการลงทุนของคุณ.";
		mes "ดันเจี้ยน Agit จะเปิดให้ทุกคนเข้าชมโดยมีค่าธรรมเนียมแรกเข้าเล็กน้อยใน "+.@realm$+"";
		next;
		mes .@n$;
		mes "กรุณาเยี่ยมชมให้บ่อยที่สุดเนื่องจากค่าธรรมเนียมแรกเข้าจะถูกบันทึกไว้เป็นการลงทุนสำหรับภูมิภาค";
		mes "คุณอยากเข้าดันเจี้ยน Agit ไหม?";
		next;
		set .@i, select(.@realm$+" Dungeon 1F - 10000z:"+.@realm$+" Dungeon 2F - 10000z:ยกเลิก");
		if (.@i == 3) {
			mes .@n$;
			mes "ยกเลิก.";
			close;
		}
		if (Zeny < 10000) {
			mes .@n$;
			mes "ฉันกำลังเก็บค่าธรรมเนียมแรกเข้าเล็กน้อย";
			mes "การบริจาคของคุณจะถูกบันทึกไว้สำหรับการลงทุนครั้งต่อไป";
			mes "ค่าเข้า 10,000z.";
			close;
		}
		mes .@n$;
		mes "ฉันกำลังส่งคุณไปที่ "+.@realm$+" Agit Dungeon "+.@i+"F.";
		set Zeny, Zeny-10000;
		setd "$fund_"+.@npc$+"_extra", getd("$fund_"+.@npc$+"_extra") + 1;
		close2;
		if (compare(strnpcinfo(2),"alde"))
			warp "gld_dun02"+((.@i == 2)?"_2":""),32,122;
		else if (compare(strnpcinfo(2),"gef"))
			warp "gld_dun04"+((.@i == 2)?"_2":""),39,258;
		else if (compare(strnpcinfo(2),"pay"))
			warp "gld_dun01"+((.@i == 2)?"_2":""),186,165;
		else if (compare(strnpcinfo(2),"prt"))
			warp "gld_dun03"+((.@i == 2)?"_2":""),164,268;
		end;
	} else if (.@status == 2) {
		mes .@n$;
		mes ""+.@realm$+" Agit Dungeon 2F พร้อมใช้งานแล้ว แต่เนื่องจากไม่ได้เชื่อมต่อกับ ประตูนรก";
		mes "ดันเจี้ยน Agit นั้นสามารถเข้าถึงได้โดยกิลด์ Agit เท่านั้น";
		close;
	} else {
		mes .@n$;
		mes .@realm$+" ไม่ได้ถูกเลือกให้เชื่อมต่อกับเกตในครั้งนี้";
		mes "โปรดรอระยะเวลาการลงทุนครั้งถัดไป";
		close;
	}
	end;
}

-	script	Investment Status Board	-1,{
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 1) {
		if (compare(strnpcinfo(4),"alde")) {
			set .@npc$,"alde";
			set .@realm$,"Luina";
		} else if (compare(strnpcinfo(4),"gef")) {
			set .@npc$,"gef";
			set .@realm$,"Britoniah";
		} else if (compare(strnpcinfo(4),"pay")) {
			set .@npc$,"pay";
			set .@realm$,"Greenwood Lake";
		} else if (compare(strnpcinfo(4),"prt")) {
			set .@npc$,"prt";
			set .@realm$,"Valkyrie Realm";
		}
		set .@notice, $agit_result_notice;
		if (.@notice < 1 || .@notice > 12) {
			mes "ขณะนี้ไม่มีจำนวนเงินลงทุนที่มีอยู่";
			close;
		}
		mes callfunc("F_GetNumSuffix",.@notice)+" ทั้งหมด (หน่วยเป็น 10,000z)";
		for(set .@i,1; .@i<=5; set .@i,.@i+1) {
			set .@map$, .@npc$+"g_cas0"+.@i;
			set .@fund[.@i], getd("$@vfund_"+.@npc$+"0"+.@i);
			mes getcastlename(.@map$)+" "+getguildname(getcastledata(.@map$,CD_GUILD_ID))+" Guild: "+.@fund[.@i];
		}
		set .@fund[0], getd("$@vfund_"+.@npc$+"_extra");
		mes .@realm$+" ปกติ: "+.@fund[0];
		mes .@realm$+" การลงทุนรวมทั้งหมด: "+(.@fund[0]+.@fund[1]+.@fund[2]+.@fund[3]+.@fund[4]+.@fund[5]);
		close;
	} else if (.@invest_status == 2) {
		mes "!- ประกาศ -!";
		mes "ไม่มีจำนวนเงินเนื่องจากจำนวนเงินลงทุนทั้งหมดกำลังถูกคำนวณ";
		mes "กรุณารอประกาศผล";
		close;
	} else if (.@invest_status == 3) {
		setarray .@invest_result[0], $prt_invest_result,$pay_invest_result,$gef_invest_result,$alde_invest_result;
		setarray .@invest_region$[0],  "Valkyrie Realm",  "Greenwood Lake",       "Britoniah",            "Luina";
		mes "!- ผลการลงทุนของภูมิภาค Agit ทั้งหมด -!";
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@invest_result[.@i] == 1)
				mes "^aaaaaa["+.@invest_region$[.@i]+"] ต่ำกว่าขั้นต่ำ^000000";
			else if (.@invest_result[.@i] == 2) {
				mes "["+.@invest_region$[.@i]+"] สำเร็จขั้นต่ำ";
				mes "-> Guild Dungeon Deepest Layer (2F) ใช้ได้";
			} else if (.@invest_result[.@i] == 3) {
				mes "^4d4dff["+.@invest_region$[.@i]+"] บรรลุสูงสุด^000000";
				mes "-> ประตูนรกพร้อมใช้งาน";
			} else
				mes "ไม่มีข้อมูลสำหรับ ["+.@invest_region$[.@i]+"].";
		}
		close;
	} else {
		mes "!- ประกาศ -!";
		mes "จะไม่มีการประกาศข้อมูลการลงทุนในขณะนี้";
		close;
	}
}

// Inside Castles - Gate Managers
//============================================================
-	script	Gate Manager#castle	-1,{

	set .@id, atoi(charat(strnpcinfo(2),getstrlen(strnpcinfo(2))-1));
	if (compare(strnpcinfo(2),"alde")) {
		set .@npc$,"alde";
		set .@realm$,"Luina";
	} else if (compare(strnpcinfo(2),"gef")) {
		set .@npc$,"gef";
		set .@realm$,"Britoniah";
	} else if (compare(strnpcinfo(2),"pay")) {
		set .@npc$,"pay";
		set .@realm$,"Greenwood Lake";
	} else if (compare(strnpcinfo(2),"prt")) {
		set .@npc$,"prt";
		set .@realm$,"Valkyrie Realm";
	}

	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (getcharid(2) != .@GID) {
		mes "[Gate Manager]";
		mes "ดูเหมือนคุณจะไม่ได้เป็นสมาชิกกิลด์ที่เป็นเจ้าของปราสาทแห่งนี้";
		mes "หากคุณต้องการความช่วยเหลือ กรุณาเยี่ยมชมสมาชิกกิลด์ของเราที่ "+.@realm$+" Square";
		close;
	}
	mes "[Gate Manager]";
	mes "สวัสดี ฉันคือผู้จัดการประตูจากแผนกวาร์ป Cat Paw Merchant Guild";
	mes " วันนี้ฉันจะช่วยคุณได้อย่างไร?";
	next;
	switch(select("ลงทุนเพื่อเชื่อมต่อประตู:การเข้าถึงดันเจี้ยนเลเยอร์ที่ลึกที่สุด:การเข้าถึงประตูนรก:ยกเลิก")) {
	case 1:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
			mes "[Gate Manager]";
			mes "คุณมีไอเทมมากเกินไปที่จะรับ Thank Invest Tickets เป็นของขวัญเล็กๆ น้อยๆ สำหรับการลงทุนของคุณ";
			mes "กรุณากลับมาหลังจากเพิ่มพื้นที่ในคลังของคุณแล้ว";
			close;
		}
		if (countitem(6488) >= 4800) {
			mes "[Gate Manager]";
			mes "คุณมี Thank Invest Ticketss มากเกินไปที่จะรับอีกต่อไป";
			mes "ไม่สามารถให้ตั๋วแก่คุณได้อีกต่อไป โปรดใช้ตั๋วของคุณและกลับมา";
			mes "ตั๋วสามารถแลกเปลี่ยนกับไอเทมต่างๆ ใน Malangdo";
			close;
		}
		if (callfunc("F_Invest_Status") != 1) {
			mes "[Gate Manager]";
			mes "เราไม่ได้รับการลงทุนในขณะนี้";
			mes "กรุณากลับมาในระหว่างระยะเวลาการลงทุน";
			close;
		}
		if (getcharid(2) != .@GID) {
			mes "[Gate Manager]";
			mes "ดูเหมือนคุณจะไม่ได้เป็นสมาชิกกิลด์ที่เป็นเจ้าของปราสาทแห่งนี้";
			mes "โปรดใช้การลงทุนปกติจาก "+.@realm$+" Square";
			close;
		}
		mes "[Gate Manager]";
		mes "ดำเนินการลงทุนภายใต้ชื่อกิลด์ของคุณ";
		callfunc "F_Invest_Abyss","$fund_"+.@npc$+"0"+.@id,"[Gate Manager]";
		end;
	case 2:
		callsub L_Enter,1,.@realm$,.@npc$;
		mes "[Gate Manager]";
		mes "คุณสนใจที่จะสำรวจ "+.@realm$+" Guild Dungeon Deepest Layer (2F).";
		mes "ให้ฉันแนะนำคุณ ขอให้โชคดี.";
		close2;
		if (compare(strnpcinfo(2),"alde")) {
			switch(.@id) {
				case 1: warp "gld_dun02_2",32,122; end;
				case 2: warp "gld_dun02_2",79,30; end;
				case 3: warp "gld_dun02_2",165,38; end;
				case 4: warp "gld_dun02_2",160,148; end;
				case 5: warp "gld_dun02_2",103,169; end;
			}
		} else if (compare(strnpcinfo(2),"gef")) {
			switch(.@id) {
				case 1: warp "gld_dun04_2",39,258; end;
				case 2: warp "gld_dun04_2",125,270; end;
				case 3: warp "gld_dun04_2",268,251; end;
				case 4: warp "gld_dun04_2",268,108; end;
				case 5: warp "gld_dun04_2",230,35; end;
			}
		} else if (compare(strnpcinfo(2),"pay")) {
			switch(.@id) {
				case 1: warp "gld_dun01_2",186,165; end;
				case 2: warp "gld_dun01_2",54,165; end;
				case 3: warp "gld_dun01_2",54,39; end;
				case 4: warp "gld_dun01_2",186,39; end;
				case 5: warp "gld_dun01_2",223,202; end;
			}
		} else if (compare(strnpcinfo(2),"prt")) {
			switch(.@id) {
				case 1: warp "gld_dun03_2",28,251; end;
				case 2: warp "gld_dun03_2",164,268; end;
				case 3: warp "gld_dun03_2",164,179; end;
				case 4: warp "gld_dun03_2",268,203; end;
				case 5: warp "gld_dun03_2",199,28; end;
			}
		}
		end;
	case 3:
		callsub L_Enter,2,.@realm$,.@npc$;
		mes "[Gate Manager]";
		mes "ฉันจะแนะนำคุณไปยัง ช่องว่างแห่งนรก ที่เชื่อมต่อกับ "+.@realm$+", the Abyss Gate 'วิถีแห่งนักรบ'.";
		close2;
		if (compare(strnpcinfo(2),"alde")) {
			switch(.@id) {
				case 1: warp "gld2_ald",175,41; end;
				case 2: warp "gld2_ald",77,64; end;
				case 3: warp "gld2_ald",46,127; end;
				case 4: warp "gld2_ald",104,246; end;
				case 5: warp "gld2_ald",241,156; end;
			}
		} else if (compare(strnpcinfo(2),"gef")) {
			switch(.@id) {
				case 1: warp "gld2_gef",28,199; end;
				case 2: warp "gld2_gef",217,46; end;
				case 3: warp "gld2_gef",171,195; end;
				case 4: warp "gld2_gef",30,67; end;
				case 5: warp "gld2_gef",115,40; end;
			}
		} else if (compare(strnpcinfo(2),"pay")) {
			switch(.@id) {
				case 1: warp "gld2_pay",33,112; end;
				case 2: warp "gld2_pay",119,27; end;
				case 3: warp "gld2_pay",205,111; end;
				case 4: warp "gld2_pay",175,221; end;
				case 5: warp "gld2_pay",61,221; end;
			}
		} else if (compare(strnpcinfo(2),"prt")) {
			switch(.@id) {
				case 1: warp "gld2_prt",10,27; end;
				case 2: warp "gld2_prt",14,247; end;
				case 3: warp "gld2_prt",259,236; end;
				case 4: warp "gld2_prt",226,25; end;
				case 5: warp "gld2_prt",97,11; end;
			}
		}
		end;
	case 4:
		mes "[Gate Manager]";
		mes "ยกเลิก.";
		close;
	}
	end;

L_Enter:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 3) {
		set .@status, getd("$"+getarg(2)+"_invest_result");
		if ((getarg(0) == 1 && .@status == 2) || .@status == 3)
			return;
		else {
			mes "[Gate Manager]";
			mes getarg(1)+" ครั้งนี้ไม่ได้ถูกเลือกให้เชื่อมต่อกับประตู";
			mes "โปรดรอระยะเวลาการลงทุนครั้งถัดไป";
			close;
		}
	} else if (.@invest_status < 3) {
		mes "[Gate Manager]";
		mes "ขณะนี้ไม่มีประตูดันเจี้ยนที่เชื่อมต่ออยู่";
		mes "ประตูจะเชื่อมต่อทันทีที่ผลการลงทุนพร้อม";
		mes "กรุณารอสักครู่";
		close;
	} else {
		mes "[Gate Manager]";
		mes "ประตูจะหายไปเร็วๆ นี้";
		mes "ดันเจี้ยนไม่สามารถเข้าถึงได้อีกต่อไป";
		mes "โปรดลองอีกครั้งในครั้งต่อไป";
		close;
	}
}

// Item Investments :: tooja_namis
//============================================================
malangdo,218,126,4	script	Cat Paw Merchants Notice	857,{
	mes "<< ประกาศเกี่ยวกับ Cat Paw Merchants >>";
	mes "'^C379CEInvest Unused Items for Prizes!^000000'";
	next;
	switch(select("อ่านประกาศ:Check Event 1:Check Event 2")) {
	case 1:
		mes "- ประกาศ -";
		mes "สวัสดีลูกค้า Cat Paw Merchants ทุกท่าน! ด้วยความรักของคุณ พวกเรา Cat Paw Merchants เติบโตขึ้นทุกวัน ผู้บริหารและพนักงานทุกคนสัญญาว่าจะพยายามอย่างเต็มที่เพื่อก้าวต่อไปในขอบเขตและการพัฒนาของเรา";
		next;
		mes "- ประกาศกิจกรรม! -";
		mes " กิจกรรมที่เรียกว่า '^C379CEInvest Unused Items for Prizes!^000000' กำลังดำเนินอยู่ ซึ่งคุณสามารถแลกเปลี่ยนรายการที่ไม่ได้ใช้ในคลังของคุณเป็น Investment Certificate of the Cat Paw Merchants";
		next;
		mes "รายการที่สามารถแลกเปลี่ยนได้จะถูกประกาศในแผ่นงานแยกต่างหาก ขอบคุณที่สละเวลา";
		mes "- จากพนักงานทั้งหมดของ Cat Paw Merchants -";
		next;
		mes "มีคำเตือนเล็กๆที่ด้านล่าง";
		mes " ";
		mes "^FF0000กิจกรรมนี้สามารถเปลี่ยนแปลงหรือยกเลิกได้เนื่องจากสถานการณ์ของพ่อค้า^000000";
		close;
	case 2:
		mes "'^C379CEInvest Unused Items for Prizes!^000000'";
		mes "มีรายละเอียดของกิจกรรมที่ 1";
		next;
		setarray .@event1$[0],
			"Wings of the Insect Queen","Crown of the Fly King","I hear Dragon Scales are tough 01","The Meaning of the Mother's Nightmare",
			"To refine ore","Hekekek Orcs, Orcs!!","Study of Yin-Yang","Secrets of the Pyramid King","Tick-tock the time goes",
			"Planting trees on Mellow Island","Ancient language research","Weapons made of sharp teeth","Toy boss doll 01",
			"A tiger is just a cat after all","The shattered spear and shield?","Toy boss doll 02","Best in summer! Cold scales",
			"Piece of Thanatos' armor","I hear Dragon Scales are tough 02","The identity of the creepy eye","If only I could see!";
		set .@menu$, "Quit:"+implode(.@event1$[0],":");
		while(1) {
			set .@i, select(.@menu$)-1;
			switch(.@i) {
				case 0: close;
				case 1: set .@str$,"2 Pieces of Queen's Wing"; break;
				case 2: set .@str$,"2 Broken Crowns"; break;
				case 3: set .@str$,"2 Fire Dragon Scales"; break;
				case 4: set .@str$,"3 Mother's Nightmares"; break;
				case 5: set .@str$,"5 Gemstones"; break;
				case 6: set .@str$,"5 Heroic Emblems"; break;
				case 7: set .@str$,"3 Taeguk Plates"; break;
				case 8: set .@str$,"3 Broken Pharaoh Symbols and 3 Tutankhamen's Masks"; break;
				case 9: set .@str$,"2 Pocket Watches"; break;
				case 10: set .@str$,"3 Young Twigs"; break;
				case 11: set .@str$,"5 Rossata Pieces"; break;
				case 12: set .@str$,"5 Fangs of Garm"; break;
				case 13: set .@str$,"3 Baphomet Dolls"; break;
				case 14: set .@str$,"1 Tiger's Footskin and 5 Tiger Skins"; break;
				case 15: set .@str$,"3 Broken Shield Pieces and 3 Shining Spear Blades"; break;
				case 16: set .@str$,"3 Osiris Dolls"; break;
				case 17: set .@str$,"1 Ice Scale"; break;
				case 18: set .@str$,"1 Skeletal Armor Piece"; break;
				case 19: set .@str$,"1 Darkred Scale Piece"; break;
				case 20: set .@str$,"2 Wills of Red Darkness"; break;
				case 21: set .@str$,"2 Foolishness of the Blind"; break;
			}
			mes "["+.@event1$[.@i-1]+"]";
			mes .@str$+" for one Cat Paw Merchants Investment Certificate.";
			next;
		}
	case 3:
		mes "Look out for the next '^C379CEInvest Unused Items for Prizes!^000000' Event 2!! Coming up real soon!";
		close;
	}
}

malangdo,218,123,1	script	Namis#invest	545,4,4,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again after you -";
		mes "- lose some weight. -";
		close;
	}
	emotion ET_HUK;
	emotion ET_FRET;
	emotion ET_ANGER;
	mes "[Namis]";
	mes "Purr-Purr-Purrr-Purrrr-Pur~ Meow!!";
	mes "Wh-- What? How rude. You scared me.";
	next;
	switch(select("What do you need? I'll invest!:What's the purpose of this event?:Were you asleep?")) {
	case 1:
		break;
	case 2:
		mes "[Namis]";
		mes "So, another pushover in contract with the Cat Paw Merchants?";
		next;
		select("Huh? What! You calling me a pushover?");
		mes "[Namis]";
		mes "I'll say this just once, so you listen good!";
		mes "We, the Cat Paw Merchants have planned an event for loyal workers like yourself.";
		next;
		mes "[Namis]";
		mes "Run along to your storage and bring back rotting antiques to us and we'll exchange them for the oh-so valuable 'Cat Paw Merchants Investment Certificate'.";
		next;
		mes "[Namis]";
		mes "It will be wise for you to do so because the Cat Paw Merchants are doing this at a loss!";
		next;
		mes "[Namis]";
		mes "Details are on the notice.";
		close;
	case 3:
		mes "[Namis]";
		mes "Hick- Slurp-";
		mes "Remember, human! There's no other cat who works as hard as I do.";
		close;
	}
	mes "[Namis]";
	mes "You must be here to participate in the '^C379CEInvest Unused Items for Prizes!^000000' event of the Cat Paw Merchants.";
	next;
	mes "[Namis]";
	mes "So, which event do you wish to participate in?";
	next;
	switch(select("Not interested:Event 1 Items:Event 2 Items")) {
	case 1:
		mes "[Namis]";
		mes "Don't you mess with a busy cat!";
		close;
	case 2:
		mes "[Namis]";
		mes "So you're in for Event 1.";
		mes "Choose from the shown list.";
		next;
		setarray .@event1$[0],
			"Wings of the Insect Queen","Crown of the Fly King","I hear Dragon Scales are tough 01","The Meaning of the Mother's Nightmare",
			"To refine ore","Hekekek Orcs, Orcs!!","Study of Yin-Yang","Secrets of the Pyramid King","Tick-tock the time goes",
			"Planting trees on Mellow Island","Ancient language research","Weapons made of sharp teeth","Toy boss doll 01",
			"A tiger is just a cat after all","The shattered spear and shield?","Toy boss doll 02","Best in summer! Cold scales",
			"Piece of Thanatos' armor","I hear Dragon Scales are tough 02","The identity of the creepy eye","If only I could see!";
		set .@i, select(implode(.@event1$[0],":"))-1;
		switch(.@i+1) {
			case 1: callsub L_Check,"2 Pieces of Queen's Wing",6326,2,"Wings of the Insect Queen? What a pitiful name. I would've given some cool names!",.@event1$[.@i];
			case 2: callsub L_Check,"2 Broken Crowns",7754,2,"For crying out loud! Those cats working on these events must be playing around. Can't they think of a better name?",.@event1$[.@i];
			case 3: callsub L_Check,"2 Fire Dragon Scales",7451,2,"Dragon scales? Well... I guess they are tough. But what are they going to use them for?",.@event1$[.@i];
			case 4: callsub L_Check,"3 Mother's Nightmares",7020,3,"Even though I don't have the authority to find out, I wonder what they're doing with the collected items?",.@event1$[.@i];
			case 5: callsub L_Check,"5 Gemstones",7300,5,"Something useful for a change. The ore will end up as nice pieces of jewelry.",.@event1$[.@i];
			case 6: callsub L_Check,"5 Heroic Emblems",968,5,"Good, good. This is the kind of name I wanted!",.@event1$[.@i];
			case 7: callsub L_Check,"3 Taeguk Plates",7169,3,"Taking in something like this, maybe they're up to something dangerous.",.@event1$[.@i];
			case 8: callsub L_Check,"3 Broken Pharaoh Symbols and 3 Tutankhamen's Masks",7113,3,"I hear the pyramids are in Morocc, but I've never been there. Some humans call it Morco.",.@event1$[.@i],7114,3;
			case 9: callsub L_Check,"2 Pocket Watches",7513,2,"Those lazy merchants, why do they need pocket watches they don't even use?",.@event1$[.@i];
			case 10: callsub L_Check,"3 Young Twigs",7018,3,"Mellow Island has a climate like with where we cats used to live. But the trees die out sometimes due to it being an island.",.@event1$[.@i];
			case 11: callsub L_Check,"5 Rossata Pieces",7211,5,"I guess they collect these because humans have them, but what's the use of researching ancient languages?",.@event1$[.@i];
			case 12: callsub L_Check,"5 Fangs of Garm",7036,5,"Weak humans need sharp weapons, but cats already have nice and sharp claws, so why are we collecting these!!",.@event1$[.@i];
			case 13: callsub L_Check,"3 Baphomet Dolls",750,3,"Baphomet dolls are great for playing around with.",.@event1$[.@i];
			case 14: callsub L_Check,"1 Tiger's Footskin and 5 Tiger Skins",1030,1,"Did you know that tigers are cats too? Therefore, cats are better than tigers.",.@event1$[.@i],1029,5;
			case 15: callsub L_Check,"3 Broken Shield Pieces and 3 Shining Spear Blades",7108,3,"I don't know what they want with these.",.@event1$[.@i],7109,3;
			case 16: callsub L_Check,"3 Osiris Dolls",751,3,"Shame~ I never got to play with an Osiris doll.",.@event1$[.@i];
			case 17: callsub L_Check,"1 Ice Scale",7562,1,"Mellow Island is a fabulous place to live. But the ships are too hot.",.@event1$[.@i];
			case 18: callsub L_Check,"1 Skeletal Armor Piece",7450,1,"Ewww... Why would they collect something so creepy?",.@event1$[.@i];
			case 19: callsub L_Check,"1 Darkred Scale Piece",6091,1,"Dragon scales? Well... I guess they are tough. But what are they going to use them for?",.@event1$[.@i];
			case 20: callsub L_Check,"2 Wills of Red Darkness",7566,2,"Interesting! Does it scream or something when I poke it in the eye?",.@event1$[.@i];
			case 21: callsub L_Check,"2 Foolishness of the Blind",7021,2,"This blind man is foolish in not knowing that gathering 300 cats cures blindness...",.@event1$[.@i];
		}
	case 3:
		mes "[Namis]";
		mes "Participate in Event 2?";
		mes "It is not yet in motion. Wait for it!";
		close;
	}
	end;
L_Check:
	set .@args, getargcount();
	if (.@args > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (countitem(getarg(1)) < getarg(2) || .@items == 1) {
		mes "[Namis]";
		mes "You don't have the items?";
		mes getarg(0)+" exchange for one Cat Paw Merchants Investment Certificate.";
		next;
		mes "[Namis]";
		mes "Don't bother me and look up the details on the board over there!";
		close;
	}
	mes "[Namis]";
	mes "'^8E5601"+getarg(4)+" Event^000000'";
	mes "Seems to be the right items.";
	mes getarg(3);
	mes "Oh! Almost forgot. Will you invest?";
	next;
	if(select("Invest:Don't Invest") == 2) {
		mes "[Namis]";
		mes "Don't you mess with a busy cat!";
		close;
	}
	delitem getarg(1),getarg(2);
	if (.@items == 2)
		delitem getarg(5),getarg(6);
	getitem 6489,1; //Cats_Invest_Certif
	mes "[Namis]";
	mes "Here's your Certificate. Always be grateful to the Merchants for these events!";
	close;
OnTouch:
	if (rand(2))
		emotion ET_SLEEPY;
	else
		specialeffect EF_SLEEPATTACK;
	end;
}

// Investment Rewards :: tooja_japanki
//============================================================
malangdo,215,119,4	script	Thanks Ticket Machine	564,{
	if (MaxWeight - Weight < 4500 || checkweight(1201,1) == 0) {
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again after you -";
		mes "- lose some weight. -";
		close;
	}
	mes "[Vending Machine Notice]";
	mes "^6815EA1. Don't kick the machine please. @_@";
	mes "2. You can only insert Invest Thanks Ticket.";
	mes "3. About the Jelly Box, if you open it, it gives you a random item.";
	mes "4. Hope you enjoy~^000000";
	mes "- Mr. Cat, the Machine Owner -";
	next;
	switch(select("Finish:Purchase:Red Paw Jelly Bag (5 Tickets):Black Paw Jelly Bag (50 Tickets)")) {
	case 1:
		close;
	case 2:
		mes "The Vending Machine is selling items in the following list.";
		next;
		switch(select("Finish:1 Siege Arrow Quiver A (2 Tickets):1 Siege Arrow Quiver S (2 Tickets):30 White Potion (12 Tickets):30 White Slim Potion (14 Tickets):10 Dark Water (20 Tickets):20 Siege_Violet_Potion (30 Tickets):10 Coldproof Potion (30 Tickets):10 Thunderproof Potion (30 Tickets):10 Earthproof Potion (30 Tickets):10 Fireproof Potion (30 Tickets):10 Elemental Converter[Fire] (30 Tickets):10 Elemental Converter[Water] (30 Tickets):10 Elemental Converter[Wind] (30 Tickets):10 Elemental Converter[Earth] (30 Tickets)")) {
			case 1: close;
			case 2: callsub L_Purchase,2,12678,1; //Siege_Arrow_Quiver_A
			case 3: callsub L_Purchase,2,12677,1; //Siege_Arrow_Quiver_S
			case 4: callsub L_Purchase,12,504,30; //White_Potion
			case 5: callsub L_Purchase,14,547,30; //White_Slim_Potion
			case 6: callsub L_Purchase,20,12020,10; //Water_Of_Darkness
			case 7: callsub L_Purchase,30,11547,20; //Woe_Violet_Potion
			case 8: callsub L_Purchase,30,12119,10; //Resist_Water
			case 9: callsub L_Purchase,30,12121,10; //Resist_Wind
			case 10: callsub L_Purchase,30,12120,10; //Resist_Earth
			case 11: callsub L_Purchase,30,12118,10; //Resist_Fire
			case 12: callsub L_Purchase,30,12114,10,"Elemental Converter[Fire]"; //Elemental_Fire
			case 13: callsub L_Purchase,30,12115,10,"Elemental Converter[Water]"; //Elemental_Water
			case 14: callsub L_Purchase,30,12117,10,"Elemental Converter[Wind]"; //Elemental_Wind
			case 15: callsub L_Purchase,30,12116,10,"Elemental Converter[Earth]"; //Elemental_Earth
		}
		end;
	case 3:
		mes "Red Paw Jelly Bag Button chosen. Need to insert 5 ^FF0000Invest Thanks Ticket^000000 and a random item will appear.";
		next;
		if (countitem(6488) < 5) {
			mes "You don't have enough ^FF0000Invest Thanks Ticket^000000 to proceed.";
			close;
		}
		delitem 6488,5; //Thanks_Invest_Ticket
		set .@i, rand(1,10000);
		     if (.@i <= 100) callsub L_Bag,12531,1; //White_Potion_Box
		else if (.@i <= 200) callsub L_Bag,12549,1; //White_Slim_Pot_Box2
		else if (.@i <= 250) callsub L_Bag,11549,10; //Woe_Blue_Potion
		else if (.@i <= 660) callsub L_Bag,617,1; //Old_Violet_Box
		else if (.@i <= 710) callsub L_Bag,12532,1; //Royal_Jelly_Box2
		else if (.@i <= 760) callsub L_Bag,12533,1; //Blue_Herb_Box2
		else if (.@i <= 810) callsub L_Bag,505,5; //Blue_Potion
		else if (.@i <= 1110) callsub L_Bag,644,1; //Gift_Box
		else if (.@i <= 1120) callsub L_Bag,607,1; //Yggdrasilberry
		else if (.@i <= 1130) callsub L_Bag,608,2; //Seed_Of_Yggdrasil
		else if (.@i <= 1140) callsub L_Bag,12292,5; //Unripe_Fruit
		else if (.@i <= 1150) callsub L_Bag,12293,5; //Dried_Yggdrasilberry
		else if (.@i <= 1250) callsub L_Bag,12679,1; //Sg_White_Potion_Box
		else if (.@i <= 1471) callsub L_Bag,603,1; //Old_Blue_Box
		else if (.@i <= 1571) callsub L_Bag,12550,1,"Poison Bottle Box(30)"; //Poison_Bottle_Box2
		else if (.@i <= 1671) callsub L_Bag,12020,10; //Water_Of_Darkness
		else if (.@i <= 1871) callsub L_Bag,12030,5; //Box_Of_Grudge
		else if (.@i <= 2071) callsub L_Bag,12031,5; //Sleepy_Box
		else if (.@i <= 2371) callsub L_Bag,12033,2; //Box_Of_Sunlight
		else if (.@i <= 2571) callsub L_Bag,12114,5,"Elemental Converter[Fire]"; //Elemental_Fire
		else if (.@i <= 2771) callsub L_Bag,12115,5,"Elemental Converter[Water]"; //Elemental_Water
		else if (.@i <= 2971) callsub L_Bag,12117,5,"Elemental Converter[Wind]"; //Elemental_Wind
		else if (.@i <= 3171) callsub L_Bag,12116,5,"Elemental Converter[Earth]"; //Elemental_Earth
		else if (.@i <= 3271) callsub L_Bag,12680,1; //Sg_Blue_Potion_Box
		else if (.@i <= 3471) callsub L_Bag,12118,5; //Resist_Fire
		else if (.@i <= 3671) callsub L_Bag,12119,5; //Resist_Water
		else if (.@i <= 3871) callsub L_Bag,12121,5; //Resist_Wind
		else if (.@i <= 4071) callsub L_Bag,12120,5; //Resist_Earth
		else if (.@i <= 4271) callsub L_Bag,929,20; //Immortal_Heart
		else if (.@i <= 4471) callsub L_Bag,1059,20; //Transparent_Cloth
		else if (.@i <= 4671) callsub L_Bag,905,20; //Stem
		else if (.@i <= 4871) callsub L_Bag,606,10; //Aloebera
		else if (.@i <= 5071) callsub L_Bag,609,10; //Amulet
		else if (.@i <= 5271) callsub L_Bag,710,2; //Illusion_Flower
		else if (.@i <= 5471) callsub L_Bag,576,20; //Prickly_Fruit
		else if (.@i <= 5671) callsub L_Bag,621,10; //Bitter_Herb
		else if (.@i <= 5871) callsub L_Bag,709,10; //Izidor
		else if (.@i <= 6071) callsub L_Bag,605,10; //Anodyne
		else if (.@i <= 6171) callsub L_Bag,11548,10; //Woe_White_Potion
		else if (.@i <= 6371) callsub L_Bag,706,10; //Four_Leaf_Clover
		else if (.@i <= 6571) callsub L_Bag,521,10; //Leaflet_Of_Aloe
		else if (.@i <= 6771) callsub L_Bag,707,10; //Singing_Plant
		else if (.@i <= 6971) callsub L_Bag,610,10; //Leaf_Of_Yggdrasil
		else if (.@i <= 7021) callsub L_Bag,12676,1; //Sg_Violet_Potion_Box
		else if (.@i <= 7171) callsub L_Bag,6249,10; //Savage_Meat
		else if (.@i <= 7321) callsub L_Bag,6252,10; //Wolf_Blood
		else if (.@i <= 7471) callsub L_Bag,6254,10; //Beef_Head_Meat
		else if (.@i <= 7621) callsub L_Bag,6253,10; //Cold_Ice
		else if (.@i <= 7771) callsub L_Bag,6256,10; //Ice_Fragment
		else if (.@i <= 7921) callsub L_Bag,6257,10; //Ice_Crystal
		else if (.@i <= 8071) callsub L_Bag,6260,10; //Petti_Tail
		else if (.@i <= 8309) callsub L_Bag,7932,20; //Poison_Herb_Nerium
		else if (.@i <= 8547) callsub L_Bag,7933,20; //Poison_Herb_Rantana
		else if (.@i <= 8785) callsub L_Bag,7934,20; //Poison_Herb_Makulata
		else if (.@i <= 9023) callsub L_Bag,7935,20; //Poison_Herb_Seratum
		else if (.@i <= 9261) callsub L_Bag,7936,20; //Poison_Herb_Scopolia
		else if (.@i <= 9499) callsub L_Bag,7937,20; //Poison_Herb_Amoena
		else if (.@i <= 9699) callsub L_Bag,6217,20; //Mandragora_Flowerpot
		else callsub L_Bag,12016,10; //Speed_Up_Potion
		end;
	case 4:
		mes "Black Paw Jelly Bag Button chosen. Need to insert 50 ^FF0000Invest Thanks Ticket^000000 and a random item will appear.";
		next;
		if (countitem(6488) < 50) {
			mes "You don't have enough ^FF0000Invest Thanks Ticket^000000 to proceed.";
			close;
		}
		delitem 6488,50; //Thanks_Invest_Ticket
		set .@i, rand(1,10000);
		     if (.@i <= 100) callsub L_Bag,12531,1; //White_Potion_Box
		else if (.@i <= 200) callsub L_Bag,12549,1; //White_Slim_Pot_Box2
		else if (.@i <= 250) callsub L_Bag,11549,20; //Woe_Blue_Potion
		else if (.@i <= 450) callsub L_Bag,617,1; //Old_Violet_Box
		else if (.@i <= 500) callsub L_Bag,12532,1; //Royal_Jelly_Box2
		else if (.@i <= 550) callsub L_Bag,12533,1; //Blue_Herb_Box2
		else if (.@i <= 600) callsub L_Bag,505,10; //Blue_Potion
		else if (.@i <= 800) callsub L_Bag,644,1; //Gift_Box
		else if (.@i <= 810) callsub L_Bag,607,2; //Yggdrasilberry
		else if (.@i <= 820) callsub L_Bag,608,3; //Seed_Of_Yggdrasil
		else if (.@i <= 830) callsub L_Bag,12292,10; //Unripe_Fruit
		else if (.@i <= 840) callsub L_Bag,12293,10; //Dried_Yggdrasilberry
		else if (.@i <= 940) callsub L_Bag,12679,1; //Sg_White_Potion_Box
		else if (.@i <= 1140) callsub L_Bag,603,1; //Old_Blue_Box
		else if (.@i <= 1240) callsub L_Bag,12550,1,"Poison Bottle Box(30)"; //Poison_Bottle_Box2
		else if (.@i <= 1340) callsub L_Bag,12020,20; //Water_Of_Darkness
		else if (.@i <= 1440) callsub L_Bag,12030,10; //Box_Of_Grudge
		else if (.@i <= 1540) callsub L_Bag,12031,10; //Sleepy_Box
		else if (.@i <= 1690) callsub L_Bag,12033,4; //Box_Of_Sunlight
		else if (.@i <= 1790) callsub L_Bag,12114,10,"Elemental Converter[Fire]"; //Elemental_Fire
		else if (.@i <= 1890) callsub L_Bag,12115,10,"Elemental Converter[Water]"; //Elemental_Water
		else if (.@i <= 1990) callsub L_Bag,12117,10,"Elemental Converter[Wind]"; //Elemental_Wind
		else if (.@i <= 2090) callsub L_Bag,12116,10,"Elemental Converter[Earth]"; //Elemental_Earth
		else if (.@i <= 2190) callsub L_Bag,12680,1; //Sg_Blue_Potion_Box
		else if (.@i <= 2290) callsub L_Bag,12118,10; //Resist_Fire
		else if (.@i <= 2390) callsub L_Bag,12119,10; //Resist_Water
		else if (.@i <= 2490) callsub L_Bag,12121,10; //Resist_Wind
		else if (.@i <= 2590) callsub L_Bag,12120,10; //Resist_Earth
		else if (.@i <= 2690) callsub L_Bag,929,40; //Immortal_Heart
		else if (.@i <= 2790) callsub L_Bag,1059,40; //Transparent_Cloth
		else if (.@i <= 2890) callsub L_Bag,905,40; //Stem
		else if (.@i <= 2990) callsub L_Bag,606,20; //Aloebera
		else if (.@i <= 3090) callsub L_Bag,609,20; //Amulet
		else if (.@i <= 3190) callsub L_Bag,710,4; //Illusion_Flower
		else if (.@i <= 3290) callsub L_Bag,576,40; //Prickly_Fruit
		else if (.@i <= 3390) callsub L_Bag,621,20; //Bitter_Herb
		else if (.@i <= 3490) callsub L_Bag,709,20; //Izidor
		else if (.@i <= 3590) callsub L_Bag,605,20; //Anodyne
		else if (.@i <= 3690) callsub L_Bag,11548,20; //Woe_White_Potion
		else if (.@i <= 3790) callsub L_Bag,706,20; //Four_Leaf_Clover
		else if (.@i <= 3890) callsub L_Bag,521,20; //Leaflet_Of_Aloe
		else if (.@i <= 3990) callsub L_Bag,707,20; //Singing_Plant
		else if (.@i <= 4090) callsub L_Bag,610,20; //Leaf_Of_Yggdrasil
		else if (.@i <= 4140) callsub L_Bag,12676,1; //Sg_Violet_Potion_Box
		else if (.@i <= 4240) callsub L_Bag,6249,20; //Savage_Meat
		else if (.@i <= 4340) callsub L_Bag,6252,20; //Wolf_Blood
		else if (.@i <= 4440) callsub L_Bag,6254,20; //Beef_Head_Meat
		else if (.@i <= 4540) callsub L_Bag,6253,20; //Cold_Ice
		else if (.@i <= 4640) callsub L_Bag,6256,20; //Ice_Fragment
		else if (.@i <= 4740) callsub L_Bag,6257,20; //Ice_Crystal
		else if (.@i <= 4840) callsub L_Bag,6260,20; //Petti_Tail
		else if (.@i <= 4940) callsub L_Bag,7932,40; //Poison_Herb_Nerium
		else if (.@i <= 5040) callsub L_Bag,7933,40; //Poison_Herb_Rantana
		else if (.@i <= 5140) callsub L_Bag,7934,40; //Poison_Herb_Makulata
		else if (.@i <= 5240) callsub L_Bag,7935,40; //Poison_Herb_Seratum
		else if (.@i <= 5340) callsub L_Bag,7936,40; //Poison_Herb_Scopolia
		else if (.@i <= 5440) callsub L_Bag,7937,40; //Poison_Herb_Amoena
		else if (.@i <= 5540) callsub L_Bag,6217,40; //Mandragora_Flowerpot
		else if (.@i <= 6000) callsub L_Bag,12016,20; //Speed_Up_Potion
		else if (.@i <= 6500) callsub L_Bag,2483,1; //Siege_Greave
		else if (.@i <= 7000) callsub L_Bag,2484,1; //Siege_Boots
		else if (.@i <= 7500) callsub L_Bag,2485,1; //Siege_Shoes
		else if (.@i <= 8000) callsub L_Bag,2586,1; //Siege_Manteau
		else if (.@i <= 8500) callsub L_Bag,2587,1; //Siege_Muffler
		else if (.@i <= 9000) callsub L_Bag,15046,1; //Siege_Plate
		else if (.@i <= 9500) callsub L_Bag,15047,1; //Siege_Suits
		else callsub L_Bag,15048,1; //Siege_Robe
		end;
	}
	end;

//callsub L_Purchase,<ticket cost>,<reward ID>,<reward amount>{,<reward name>};
L_Purchase:
	if (countitem(6488) < getarg(0)) {
		mes "You don't have enough ^FF0000Invest Thanks Ticket^000000 to proceed.";
		close;
	}
	delitem 6488,getarg(0); //Thanks_Invest_Ticket
	getitem getarg(1),getarg(2);
	mes "- Done! -";
	next;
	mes "The item ^0000FF"+((getargcount() > 3)?getarg(3,""):getitemname(getarg(1)))+"^000000 has been obtained.";
	close;

//callsub L_Bag,<reward ID>,<reward amount>{,<reward name>};
L_Bag:
	getitem getarg(0),getarg(1);
	mes "- Done! -";
	next;
	mes "The item ^0000FF"+((getargcount() > 2)?getarg(2,""):getitemname(getarg(0)))+"^000000 has been obtained.";
	close;
}
